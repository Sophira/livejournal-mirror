#!/usr/bin/perl
#

my $CACHE_SIZE = 10_000;

BEGIN {
    unshift @INC, "$ENV{'LJHOME'}/cgi-bin";
}

use FCGI;
use LJ::Cache;
use vars qw($dbh $dro);

require "$ENV{'LJHOME'}/cgi-bin/ljlib.pl";

$dbh = LJ::get_dbh("master");
if ($LJ::DBINFO{'slavecount'}) { $dro = LJ::get_dbh("slave"); }

my $cache = new LJ::Cache { 'maxsize' => $CACHE_SIZE };

sub make_dir
{
    my $d = shift;
    return if (-d $d);
    mkdir $d, 0777;
}
foreach my $d ("$ENV{'LJHOME'}/temp",
	       "$ENV{'LJHOME'}/temp/userpic") {
    make_dir($d);
}
my $CACHE_DIR = "$ENV{'LJHOME'}/temp/userpic";

my $hits = 0;
my $misses = 0;
my $ifmodsince = 0;    # if we just said "No" to "If-Modified-Since" HTTP header
my $timestart = time();
my %from;
my %reqmeth;
my $lastconn;

 REQUEST:
    while(FCGI::accept() >= 0) 
{
    if ($ENV{'HTTP_IF_MODIFIED_SINCE'}) {
	# we can safely do this without checking since we never re-use
	# picture IDs and don't let the contents get modified

	$ifmodsince++;

	print "Status: 304 Not Modified\n\n";
	next REQUEST;
    }

    %FORM = ();
    &get_form_data(\%FORM);
    if ($ENV{'PATH_INFO'} =~ /^\/(\w+)$/) {
	$FORM{'id'} = $1;
    }
    $reqmeth{$ENV{'REQUEST_METHOD'}}++;

    if ($FORM{'id'} =~ /^info/) {
	print "Content-type: text/html\n\n<h1>userpic stats</h1>";
	print "<PRE>";
	my $pid = $$;
	my $cached = $cache->{'size'};
	print  " cached: $cached\n";
	my $total = $hits + $misses + $ifmodsince;
	$total ||= 1;
	printf "   hits: $hits\t(%.1f%%)\n", ($hits*100/$total);
	printf " misses: $misses\t(%.1f%%)\n", ($misses*100/$total);
	printf "not mod: $ifmodsince\t(%.1f%%)\n", ($ifmodsince*100/$total);
	print "Request methods:\n";
	foreach (sort keys %reqmeth) {
	    print "  $_ = $reqmeth{$_}\n";
	}
	print "Fetch source:\n";
	foreach (keys %from) {
	    print "  $_ = $from{$_}\n";
	}

	print "pid: $$\n";
	print "USER       PID %CPU %MEM   VSZ  RSS TTY      STAT START   TIME COMMAND\n";
	foreach (split(/\n/, `ps auwx | grep $pid`)) {
	    next if ($_ =~ m/grep/);
	    print $_, "\n";
	}
	print "</PRE>";

	my $show = 400;
	print "<h2>first $show of $cache->{'size'} cache entries:</h2>\n";
	print "with seconds alive in cache shown in parentheses.<BR>";
	print "<A HREF=\"/userpic/info?sort=recent\">[sort by recently hit]</A> ";
	print "<A HREF=\"/userpic/info?sort=time\">[sort by time in cache]</A> ";
	print "<P>";

	my @items = ();

	my $iter = $cache->{'head'};
	while (defined $iter) {
	    my $node = $cache->{'items'}->{$iter};
	    my $item = $node->[1];
	    push @items, { 'id' => $iter, 
			   'time' => $item->{'time'},
		       };
	    $iter = $node->[2];
	}
	
	if ($FORM{'sort'} eq "time") {
	    @items = sort { $a->{'time'} <=> $b->{'time'} } @items;
	}
	my $count = 0;
	foreach my $it (@items) {
	    if ($count++ == $show) { last; }
	    my $age = $now - $it->{'time'};
	    print "<a href=\"/userpic/$it->{'id'}\">$it->{'id'}</a> ($age), ";
	}

	next REQUEST;
    }

    unless ($FORM{'id'} =~ /^\d+$/) {
	print "Status: 404 Not Found\n";
	print "Content-type: text/html\n\n";
	print "<h1>Not Found</h1>The requested image was not found.";
	print "\n";
	next REQUEST;
    }

    my $qid = $FORM{'id'} + 0;
    my $pic = $cache->get($qid);
    my $modnum = $qid % 1000;
    my $cache_file = "$CACHE_DIR/$modnum/$qid";
    my @s = stat($cache_file);
    my $on_disk = $s[7];

    if (! defined $pic || ! $on_disk)
    {
	$misses++;

	my $now = time();
	## every 10 seconds, revalidate/reconnect our db connections
	if (($now - $lastconn) > 10) {
	    $dbh = LJ::get_dbh("master");
	    if ($LJ::DBINFO{'slavecount'}) { $dro = LJ::get_dbh("slave"); }
	    $lastconn = $now;
	}

	my $query = "SELECT p.state, p.contenttype, pb.imagedata, UNIX_TIMESTAMP(p.picdate) AS 'lastmod' FROM userpic p, userpicblob pb WHERE p.picid=pb.picid AND p.picid=$qid";
	
	if ($on_disk) {
	    # then join with userpicblob table is unnecessary; just
	    # use version on disk (web servers cheap, db servers expensive)
	    $query = "SELECT p.state, p.contenttype, UNIX_TIMESTAMP(p.picdate) AS 'lastmod' FROM userpic p WHERE p.picid=$qid";
	}

	## try the slave database dirst, we available.
	if ($LJ::DBINFO{'slavecount'} && defined $dro) {
	    $sth = $dro->prepare($query);
	    $sth->execute;
	    unless ($dbh->err) { 
		$pic = $sth->fetchrow_hashref;
		$sth->finish;
		if (defined $pic) { $from{'slave'}++; }
	    }
	}

	## either we don't have a slave database, or it's not caught up yet. try the master now.
	if (! defined $pic) {
	    $sth = $dbh->prepare($query);
	    $sth->execute;
	    if ($dbh->err) { print "Content-type: text/plain\n\n" . $dbh->errstr;  }
	    $pic = $sth->fetchrow_hashref;
	    $sth->finish;
	    if (defined $pic) { 
		$from{'master'}++; 
	    } else {
		print "Content-type: text/html\n\nNo userpic by that id.\n"; 
		next REQUEST;
	    }
	}

	if ($pic->{'state'} eq "B") { $pic->{'imagedata'} = "x"; }
	$pic->{'length'} = length($pic->{'imagedata'});
	$pic->{'time'} = time();

	# store the picture on disk, not in memory
	if ($on_disk) {
	    # touch it if modify time is older than a day, to keep it in
	    # disk cache for tomorrow.
	    if ($s[9] < time()-86400) {  
		open (ID, ">>$cache_file");
		close ID;
	    }
	} else {
	    make_dir("$CACHE_DIR/$modnum");
	    open (ID, ">$cache_file");
	    binmode (ID);  # poor windows users
	    print ID $pic->{'imagedata'};
	    close ID;
	}
	$cache->set($qid, $pic);
    } else {
	$hits++;
    }

    if ($pic)
    {
	unless ($pic->{'imagedata'}) {
	    open (ID, $cache_file);
	    while (<ID>) {
		$pic->{'imagedata'} .= $_;
	    }
	    close ID;
	    $pic->{'length'} = length($pic->{'imagedata'});	    
	}

	print "Content-type: $pic->{'contenttype'}\n";
	print "Content-length: $pic->{'length'}\n";
	print "Cache-Control: no-transform\n";
	print "Expires: ", LJ::time_to_http(time()+3000000), "\n"; # in 3-4 months
	print "Last-Modified: ", LJ::time_to_http($pic->{'lastmod'}), "\n";
	print "\n";
	unless ($ENV{'REQUEST_METHOD'} eq "HEAD") {
	    print $pic->{'imagedata'};
	}
	delete $pic->{'imagedata'}; 
    } else {
	print "Status: 404 Not Found\n";
	print "Content-type: text/html\n\n";
	print "<h1>Not Found</h1>The requested image was not found.";
	print "\n";
    }
}

sub get_form_data 
{
    my $hashref = shift;
    my $buffer = $ENV{'QUERY_STRING'};
    
    # Split the name-value pairs
    my $pair;
    my @pairs = split(/&/, $buffer);
    my ($name, $value);
    foreach $pair (@pairs)
    {
	($name, $value) = split(/=/, $pair);
	$value =~ tr/+/ /;
        $value =~ s/%([a-fA-F0-9][a-fA-F0-9])/pack("C", hex($1))/eg;
        $name =~ tr/+/ /;
        $name =~ s/%([a-fA-F0-9][a-fA-F0-9])/pack("C", hex($1))/eg;
	$hashref->{$name} .= $hashref->{$name} ? "\0$value" : $value;
    }
}

