<?page
body<=

<?_code
#BML:cache:talkread

 use strict;
 use vars qw($r_head $r_title %FORM %ML);

 # make refs to both title and head, that work in cached or non-cached contexts
 # $_[0] is the BML $req object, $_[1] is a pre-request scratch area.
 $r_head = "";
 $r_title = "";
 my $head = $_[1] ? \$_[1]->{'head'} : \$r_head;
 my $title = $_[1] ? \$_[1]->{'title'} : \$r_title;

 my $PAGE_SIZE = $LJ::TALK_PAGE_SIZE || 25;
 my $THREADING_POINT = $LJ::TALK_THREAD_POINT || 50;

 return LJ::server_down_html() if ($LJ::SERVER_DOWN);

 my $dbs = LJ::get_dbs();
 my $dbh = $dbs->{'dbh'};
 my $dbr = $dbs->{'reader'};
 my ($sth, $sql);
 my $errtxt;

 my $pics = LJ::Talk::get_subjecticons();

 ## workaround mail client bug when don't understand quoted-printable.
 ## Only correct 'journal' if 'itemid' was also broken, to avoid the 
 ## unlikely clash with a journal name.
 if ($FORM{'itemid'} =~ s/^3D//) {
     $FORM{'journal'} =~ s/^3D//;
     $FORM{'thread'} =~ s/^3D//; 
 }

 my $r = Apache->request;

my $uri = BML::get_uri();
my $itemid;
my $new_url;

if ($uri =~ m!/(\d+)\.html$!) {
    $new_url = 1;
    $itemid = $1;
    $FORM{'itemid'} = $itemid;
    $FORM{'journal'} = $r->notes("_journal");
    BML::set_language_scope("/talkread.bml");
}

 # pre-load common strings for little speed and less typing later
 # (we're doing this *after* set_language_scope is called, because
 # two below are relative strings)
 my %T = qw(postcomments   talk.commentpost
            readcomments   talk.commentsread
            parent         talk.parentlink
            thread         talk.threadlink
            replythis      talk.replytothis
            link           talk.commentpermlink
            deleted        .subjectdeleted
            nosubject      .nosubject
            );
 foreach (keys %T) { $T{$_} = $ML{$T{$_}}; }

 my $init = LJ::Talk::init($dbs, \%FORM);
 my $u = $init->{'journalu'};

 $r->notes("journalid" => $u->{'userid'});
 $r->notes("codepath" => "bml.talkread");

 return "<?h1 $ML{'Error'} h1?><?p $init->{'error'} p?>" if $init->{'error'}; 
 return $ML{'talk.error.nojournal'} unless $u;

 my $clustered = $init->{'clustered'};
 my $thread = $init->{'thread'};
 $itemid = $init->{'itemid'}+0;

 my ($dbcs, $dbcm, $dbcr) = ($dbs, $dbh, $dbr);
 if ($clustered) {
     $dbcs = LJ::get_cluster_set($u);
     $dbcm = $dbcs->{'dbh'};
     $dbcr = $dbcs->{'reader'};
 }

 my $item = LJ::Talk::get_journal_item($dbs, $dbcs, $u, $itemid);

 if ($init->{'oldurl'} && $item) {
     $init->{'anum'} = $item->{'anum'};
     $init->{'ditemid'} = $init->{'itemid'}*256 + $item->{'anum'};
 }

 unless ($item && $item->{'anum'} == $init->{'anum'}) {
     return "<?h1 $ML{'Error'} h1?><?p $ML{'talk.error.noentry'} p?>";
 }

 my $ditemid = $init->{'ditemid'}+0;
 my $jarg = $u->{'clusterid'} ? "journal=$u->{'user'}&" : "";
 my $jargent = $u->{'clusterid'} ? "journal=$u->{'user'}&amp;" : "";

 my $talkurl = "$LJ::SITEROOT/talkread.bml?${jarg}itemid=$ditemid";
 if ($clustered) {
     $talkurl = LJ::journal_base($u) . "/$ditemid.html";
 }

 my $talkargs = sub {
     my $args = shift;
     my $sep = ($talkurl =~ /\?/) ? "&" : "?";
     return "$talkurl$sep$args";
 };

 my $ret = "";

 # redirect if account was renamed
 if ($u->{'journaltype'} eq "R") {
     LJ::load_user_props($dbs, $u, "renamedto");
     if ($u->{'clusterid'} && $u->{'renamedto'} ne "") {
         my $id = $FORM{'itemid'}+0;
         return BML::redirect(LJ::journal_base($u) . "/$id.html");
     }
 }


 my %logprops = %{$item->{'logprops'}};

 ### load users
 my ($up);  # $up = user posted journal item
 LJ::load_userids_multiple($dbs, [
                                  $item->{'posterid'} => \$up,
                                  ], [ $u ]);
 LJ::load_user_props($dbs, $up, "opt_showtopicstuff");

 LJ::text_out(\$u->{'name'});

 # check suspended
 return "<?h1 $ML{'talk.error.suspended.title'} h1?><?p $ML{'talk.error.suspended'} p?>"
    if ($u->{'statusvis'} eq "S" || $up->{'statusvis'} eq "S");

 # check deleted
 return "<?h1 $ML{'talk.error.deleted.title'} h1?><?p $ML{'talk.error.deleted'} p?>"
    if ($u->{'statusvis'} eq "D");

 my $remote = LJ::get_remote($dbs);

 ####  Check security before viewing this post
 return $errtxt unless LJ::Talk::check_viewable($dbs, $remote, $item, \%FORM, \$errtxt);

 my $showmultiform = $remote && $clustered &&
    ($remote->{'user'} eq $u->{'user'} ||
     $remote->{'user'} eq $up->{'user'} ||
     LJ::check_rel($dbs, $u, $remote, 'A'));
 my $multiform_selects = 0;  # are there select boxes?  don't show form if not.

 my $event = $item->{'event'};

 LJ::expand_embedded($dbs, $ditemid, $remote, \$event);
 LJ::CleanHTML::clean_event(\$event, $logprops{$itemid}->{'opt_preformatted'});
 BML::ebml(\$event);

 # make the title
{
    my $subject = $item->{'subject'} || $item->{'event'};
    LJ::CleanHTML::clean_subject_all(\$subject);
    $subject =~ s/\n.*//s;
    # yes, the 3 param to text_trim is chars, and length returns bytes, but
    # it works, as bytes >= chars:
    $subject = LJ::text_trim($subject, 0, length($item->{'subject'}) || 40);
    $$title = "$u->{'user'}: $subject";
}

 $ret .= "<p>";
 $ret .= "<table><tr valign='middle'>";

 my $picid;
 if ($logprops{$itemid}->{'picture_keyword'}) {
     my $qkw = $dbr->quote($logprops{$itemid}->{'picture_keyword'});
     $picid = $dbr->selectrow_array("SELECT m.picid FROM userpicmap m, keywords k ".
                                    "WHERE m.userid=$up->{'userid'} ".
                                    "AND m.kwid=k.kwid AND k.keyword=$qkw");
 }

 $picid ||= $up->{'defaultpicid'};

 my %userpics;
 if ($picid) {
     LJ::load_userpics($dbs, \%userpics, [ $picid ]);
     my $alt = $up->{'name'};
     if ($logprops{$itemid}->{'picture_keyword'}) {
         $alt .= ": $logprops{$itemid}->{'picture_keyword'}";
     }
     LJ::text_out(\$alt);
     $alt = LJ::ehtml($alt);
     $ret .= "<td><img src='$LJ::USERPIC_ROOT/$picid/$up->{'userid'}' width='$userpics{$picid}->{'width'}' ".
         "height='$userpics{$picid}->{'height'}' align='absmiddle' ".
         "hspace='3' title='$alt' alt=''></td>";
 }

 $ret .= "<td>";
 if ($up->{'user'} eq $u->{'user'}) {
     $ret .= BML::ml("talk.somebodywrote", { 'realname' => BML::eall($up->{'name'}),
                                             'userlink' => LJ::ljuser($up->{'user'}) });
 } else {
     $ret .= BML::ml("talk.somebodywrote_comm", { 'realname' => BML::eall($up->{'name'}),
                                                  'userlink' => LJ::ljuser($up->{'user'}),
                                                  'commlink' => "<?ljcomm $u->{'user'} ljcomm?>" });
 }

 my $etime = $item->{'eventtime'};
 $etime =~ s!(\d\d\d\d)-(\d\d)-(\d\d)!LJ::date_to_view_links($u, $&)!e;
 $ret .= "<br /><font size='-1'>@ $etime</font>";
 $ret .= "</td></tr></table>";

 ## standout bar
 $ret .= LJ::Talk::link_bar($dbs, { 'u' => $u, 'up' => $up, 'headref' => $head,
                                    'remote' => $remote, 'itemid' => $ditemid, });

 ## dump the log entry, unless we're browsing a thread.
 unless ($thread)
 {
     my %current;
     if ($logprops{$itemid}->{'current_mood'} || $logprops{$itemid}->{'current_moodid'}) {
         LJ::load_moods($dbs);
         $current{'Mood'} = $logprops{$itemid}->{'current_mood'};
         LJ::CleanHTML::clean_subject(\$current{'Mood'});
         my $mid = $logprops{$itemid}->{'current_moodid'};
         if ($mid) {
             my $theme = $up->{'moodthemeid'};
               LJ::load_mood_theme($dbs, $theme);
             my %pic;
             my $name = defined $LJ::CACHE_MOODS{$mid} ? $LJ::CACHE_MOODS{$mid}->{'name'} : '';
             if (LJ::get_mood_picture($theme, $mid, \%pic)) {
                 $current{'Mood'} = "<img src='$pic{'pic'}' align='absmiddle' width='$pic{'w'}' height='$pic{'h'}' vspace='1'> $name";
             } else {
                 $current{'Mood'} = $name;
             }
         }
     }
     if ($logprops{$itemid}->{'current_music'}) {
         $current{'Music'} = $logprops{$itemid}->{'current_music'};
         LJ::CleanHTML::clean_subject(\$current{'Music'});
     }

     $ret .= "<div style='margin-left: 30px'>";

     if (%current) 
     {
         $ret .= "<table border=0>\n";
         foreach (sort keys %current) {
             my $curkey = "talk.curname_" . $_;
             my $curname = BML::ml($curkey);
             $curname = "<b>Current $_:</b>" unless $curname;
             $ret .= "<tr><td align=right>$curname</td><td>$current{$_}</td></tr>\n";
         }
         $ret .= "</table><p>\n";
     }

     ### security indicator
     my $sec = "";
     if ($item->{'security'} eq "private") {
         $sec = "<?securityprivate?>";
     } elsif ($item->{'security'} eq "usemask") {
         $sec = "<?securityprotected?>";
     }

     $sec .= "<br />\n" unless $sec eq "" or $item->{'subject'};
     $ret .= $sec;

     ###
     if ($item->{'subject'}) {
         my $subject = $item->{'subject'};
         LJ::CleanHTML::clean_subject(\$subject);
         BML::ebml(\$subject);
         $ret .= "<font face='Arial,Helvetica' size='+1'><i><b>$subject</b></i></font><br />\n";
     }

     $ret .= $event;
     $ret .= "</div>";
 }


 $ret .= "<br clear='all' /><hr width='100%' size='2' align='center' />";

 my %users_to_load;
 my @posts_to_load;
 my %posts;
 my %children;

 # get all talk rows. will load needed body & subject text later.
 {
     # clustered always gets from a reader (no recent tables exist).
     # otherwise, need to decide if it's safe to get all data from a
     # slave.  if the owning post is newer than $LJ::RECENT_DAYS, then
     # obviously all replies are newer to, and they all exist on a
     # slave.  or, if slaves are full copies, then it's also okay.
     # for clustered, also try the lite role.
     my $db = $dbcr;
     my $recent = "";
     if ($clustered) {
         my $n = $u->{'clusterid'};
         $db = LJ::get_dbh("cluster${n}lite", "cluster${n}slave", "cluster$n");
     } elsif ($LJ::USE_RECENT_TABLES) {
         if ($item->{'secondsold'} < $LJ::RECENT_DAYS*86400) {
             $recent = "recent_";
         } else {
             $db = $dbh;
         }
     }

     unless ($db) {
         return $ML{'error.nodbmaintenance'};
     }

     if ($clustered) {
         $sth = $db->prepare("SELECT t.jtalkid AS 'talkid', t.posterid, u.user AS 'userpost', ".
                             "t.datepost, t.parenttalkid, t.state ".
                             "FROM talk2 t ".
                             "LEFT JOIN useridmap u ON u.userid=t.posterid ".
                             "WHERE t.journalid=$u->{'userid'} AND t.nodetype='L' ".
                             "AND t.nodeid=$itemid ORDER BY t.jtalkid");
     } else {
         $sth = $db->prepare("SELECT t.talkid, t.posterid, u.user AS 'userpost', ".
                             "t.datepost, t.parenttalkid, t.state ".
                             "FROM talk t LEFT JOIN useridmap u ON u.userid=t.posterid ".
                             "WHERE t.nodetype='L' AND t.nodeid=$itemid ".
                             "ORDER BY t.talkid");
     }
     $sth->execute;
     die $db->errstr if $db->err;
 }
 my $post_count = 0;
 while (my $post = $sth->fetchrow_hashref) {
     $post_count++ unless $post->{'state'} eq "D" ||
         ($post->{'state'} eq "S" && !LJ::Talk::can_view_screened($dbs, $remote, $u, $up, $post->{'userpost'}));
     $posts{$post->{'talkid'}} = $post;
     push @{$children{$post->{'parenttalkid'}}}, $post->{'talkid'};
 }

 # with a wrong thread number, silently default to the whole page
 $thread = 0 unless $posts{$thread};

 # we let the page size initially get bigger than normal for awhile,
 # but if it passes threading_point, then everything's in page_size
 # chunks:
 $PAGE_SIZE = $THREADING_POINT if $post_count < $THREADING_POINT;

 $children{$thread} ||= [];  # strictness doesn't allow treating undef as arrayref

 my $top_replies = $thread ? 1 : scalar(@{$children{$thread}});
 my $pages = int($top_replies / $PAGE_SIZE);
 if ($top_replies % $PAGE_SIZE) { $pages++; }

 my $page = int($FORM{'page'}) || 1;
 $page = $page < 1 ? 1 : $page > $pages ? $pages : $page;

 my @top_replies = $thread ? ($thread) : sort { $posts{$a}->{'datepost'} cmp $posts{$b}->{'datepost'} } @{$children{$thread}};
 foreach (@top_replies) {  $posts{$_}->{'_hide'} = 1; }
 my $itemfirst = $PAGE_SIZE * ($page-1) + 1;
 my $itemlast = $page==$pages ? $top_replies : ($PAGE_SIZE * $page);

 @top_replies = @top_replies[$itemfirst-1 .. $itemlast-1];

 foreach (@top_replies) {  delete $posts{$_}->{'_hide'} }

 push @posts_to_load, @top_replies;
 my @check_for_children = @posts_to_load;

 if (@posts_to_load < $PAGE_SIZE) {
     # then let's show some more!
     while (@check_for_children && @posts_to_load < $PAGE_SIZE)
     {
         my $cfc = shift @check_for_children;
         next unless defined $children{$cfc};
         foreach my $child (sort { $posts{$a}->{'datepost'} cmp $posts{$b}->{'datepost'} } @{$children{$cfc}}) {
             unless (@posts_to_load < $PAGE_SIZE) {
                 # if we exit before processing all children, put the post back
                 # in order to get subjects of children later
                 unshift @check_for_children, $cfc;
                 last;
             }
             push @check_for_children, $child;
             push @posts_to_load, $child;
         }
     }
 }

 # find all posts where only subjects are needed, for threading. these are all 
 # the descendants of current @check_for_children, but not its members.

 my (@recurse, @subjects_to_load);

 foreach (@check_for_children) { 
     next unless defined $children{$_};
     push @recurse, @{$children{$_}}; 
 }

 while (@recurse) {
     my $post = shift @recurse;
     push @subjects_to_load, $post;
     next unless defined $children{$post};
     push @recurse, @{$children{$post}}; 
 }

 unless (@posts_to_load) {
     $ret .= $ML{'.noreplies'};
 }

 ### loads more of some of the posts, uses slaves if possible.
 my ($posts_loaded, $subjects_loaded);
 if ($clustered) {
     $posts_loaded = LJ::get_talktext2($u, @posts_to_load);
     $subjects_loaded = LJ::get_talktext2($u, {'onlysubjects'=>1}, @subjects_to_load)
         if @subjects_to_load;
 } else {
     $posts_loaded = LJ::get_talktext($dbs, @posts_to_load);
     $subjects_loaded = LJ::get_talktext($dbs, {'onlysubjects'=>1}, @subjects_to_load)
         if @subjects_to_load;
 }
 foreach my $talkid (keys %{$posts_loaded}) {
     $posts{$talkid}->{'subject'} = $posts_loaded->{$talkid}->[0];
     $posts{$talkid}->{'body'} = $posts_loaded->{$talkid}->[1];
     $posts{$talkid}->{'_loaded'} = 1;
     $users_to_load{$posts{$talkid}->{'posterid'}} = 1;
 }
 foreach my $talkid (keys %{$subjects_loaded}) {
     $posts{$talkid}->{'subject'} = $subjects_loaded->{$talkid}->[0];
 }

 ### load meta-data
 my $talkid_in = join(",", @posts_to_load);
 if ($clustered) {
     LJ::load_props($dbs, "talk");
     LJ::load_talk_props2($dbcr, $u->{'userid'}, \@posts_to_load, \%posts);
 } else {
     LJ::load_talk_props($dbs, \@posts_to_load, \%posts);
 }

 if ($LJ::UNICODE) {
     foreach (@posts_to_load) {
         if ($posts{$_}->{'unknown8bit'}) {
             LJ::item_toutf8($dbs, $u, \$posts{$_}->{'subject'},
                             \$posts{$_}->{'body'},
                             {});
         }
     }
 }

 my %user;
 foreach my $talkid (sort { $a <=> $b } keys %posts)
 {
     $_ = $posts{$talkid};
     next if ($_->{'_hide'});

     if ($_->{'state'} eq "D") {
         $_->{'subject'} = $T{'deleted'};
     }
     elsif ($_->{'state'} eq "A" || ($_->{'state'} eq "S" && LJ::Talk::can_view_screened($dbs, $remote, $u, $up, $_->{'userpost'}))) {
         # if the message isn't deleted, flag its parent(s) as
         # having children to recurse into later.
         my $par = $_->{'parenttalkid'};
         while ($par) {
             $posts{$par}->{'_haschildren'}++;
             $par = $posts{$par}->{'parenttalkid'};
         }

         unless ($user{$_->{'userpost'}}) {
             $user{$_->{'userpost'}} = {};
             if ($_->{'_loaded'}) {
                 $users_to_load{$_->{'posterid'}} = 1;
             }
         }
     }
 }

 my %userpics = ();
 delete $users_to_load{0};
 if (%users_to_load) {
     my @pics_to_load;

     my %temp;
     LJ::load_userids_multiple($dbs, [ map { $_, \$temp{$_} } keys %users_to_load ]);;
     foreach (values %temp) {
         $user{$_->{'user'}} = $_;
         push @pics_to_load, $_->{'defaultpicid'} if $_->{'defaultpicid'};
     }
     LJ::load_userpics($dbs, \%userpics, [ @pics_to_load ]);
 }

 ########## make the navcrap
 my $navcrap;
 if ($pages > 1) {
     $navcrap .= "<div align='center'><font face='Arial,Helvetica' size='-1'><b>";
     $navcrap .= BML::ml('.pageofpages',{'page'=>$page, 'total'=>$pages}) . "<br />";
     my $left = "<b>&lt;&lt;</b>";
     if ($page > 1) { $left = "<a href='" . BML::self_link({ 'page' => $page-1 }) . "'>$left</a>"; }
     my $right = "<b>&gt;&gt;</b>";
     if ($page < $pages) { $right = "<a href='" . BML::self_link({ 'page' => $page+1 }) . "'>$right</a>"; }
     $navcrap .= $left . " ";
     for (my $i=1; $i<=$pages; $i++) {
         my $link = "[$i]";
         if ($i != $page) { $link = "<a href='" . BML::self_link({ 'page' => $i }) . "'>$link</a>"; }
         else { $link = "<font size='+1'><b>$link</b></font>"; }
         $navcrap .= "$link ";
     }
     $navcrap .= "$right";
     $navcrap .= "</font></div>\n";
     $navcrap = BML::fill_template("standout", { 'DATA' => $navcrap });
 }
 ####### end navcrap

 my $recurse_post = sub
 {
     my ($self_sub, $postref, $ret, $tid, $mark, $opts) = @_;
     # don't let people make circular references
     return if ($mark->{$tid});
     $mark->{$tid} = 1;

     # find child messages (replys to $tid)
    my @childs = sort { $postref->{$a}->{'datepost'} cmp $postref->{$b}->{'datepost'} }
    grep { ($postref->{$_}->{'state'} eq "A" || $postref->{$_}->{'_haschildren'} ||
            ($postref->{$_}->{'state'} eq "S" && 
             LJ::Talk::can_view_screened($dbs, $remote, $u, $up, $postref->{$_}->{'userpost'}))
           )}
    @{$opts->{'children'}->{$tid}};

    return unless ($tid || @childs);

    my $post = $postref->{$tid};

     my $bgcolor = ($opts->{'depth'} % 2) ? "emcolorlite" : "emcolor";
     $bgcolor = BML::get_template_def($bgcolor);
     if ($post->{'state'} eq "S") {
         $bgcolor = BML::get_template_def("screenedbarcolor") || $bgcolor;
     }

    # display new or old first?
    if ($opts->{'newfirst'}) { @childs = reverse @childs; }

    if ($tid)
    {
        my $dtid = $clustered ? ($tid * 256 + $init->{'anum'}) : $tid;

        my $datepost = "<font size='-1'>" . substr($post->{'datepost'}, 0, 16) . "</font>";
        my $userpost = $post->{'userpost'};
        my $user;
        if ($post->{'deleted_poster'}) {
            $user = BML::ml('.deleteduser', {'username'=>$post->{'deleted_poster'}});
        }
        else {
            $user = $ML{'.anonuser'};
        }

        my $canviewscr = LJ::Talk::can_view_screened($dbs, $remote, $u, $up, $userpost);

        if ($post->{'state'} eq "D") {
            $$ret .= "<p><a name='t$dtid'></a><table><tr>";
            $$ret .= "<td><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
            $$ret .= "<td>$ML{'.deletedpost'}</td></tr></table>\n";
        } elsif ($post->{'state'} eq "S" && !$canviewscr) {
            $$ret .= "<p><a name='t$dtid'></a><table><tr>";
            $$ret .= "<td><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
            my $screenedtext = $ML{'.screenedpost'};
            $$ret .= "<td>$screenedtext</td></tr></table>\n";
        } elsif ($userpost && $user{$userpost}->{'statusvis'} eq "S") {
            $$ret .= "<p><a name='t$dtid'></a><table><tr>";
            $$ret .= "<td><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
            $$ret .= "<td>$ML{'.replysuspended'}";
            if (LJ::Talk::can_delete($dbs, $remote, $u, $up, $userpost)) {
                $$ret .= " <a href='/delcomment.bml?${jargent}id=$dtid'>" . LJ::img("btn_del", "", { 'align' => 'absmiddle', 'hspace' => 2, 'vspace' => }) . "</a>";
            }
            $$ret .= "</td></tr></table>\n";
        } else {
            if ($userpost) {
                $user = LJ::ljuser($userpost);
            }
            my $icon = LJ::Talk::show_image($pics, $post->{'subjecticon'});

            if ($post->{'_loaded'}) {
                $$ret .= "<a name='t$dtid'></a><table width='100%' class='talk-comment'><tr>";
                $$ret .= "<td rowspan='2'><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
                $$ret .= "<td bgcolor='$bgcolor' width='100%'>";
                if ($userpost) {
                    my $picid = $user{$userpost}->{'defaultpicid'};
                    my $pickw = $post->{'picture_keyword'};
                    if ($pickw) {
                        $picid = $user{$userpost}->{'_pictures'}->{$pickw};
                        unless ($picid) {
                            my $qkw = $dbr->quote($pickw);
                            my $sth = $dbr->prepare("SELECT m.picid FROM userpicmap m, keywords k ".
                                                    "WHERE m.userid=$post->{'posterid'} ".
                                                    "AND m.kwid=k.kwid AND k.keyword=$qkw");
                            $sth->execute;
                            my ($alt_picid) = $sth->fetchrow_array;
                            if ($alt_picid) {
                                LJ::load_userpics($dbs, \%userpics, [ $alt_picid ]);
                                $picid = $alt_picid;
                                $user{$userpost}->{'_pictures'}->{$pickw} = $picid;
                            }
                        }
                    }
                    $picid ||= $user{$userpost}->{'defaultpicid'};
                    if ($picid) {
                        my $alt = $user{$userpost}->{'name'};
                        if ($post->{'picture_keyword'}) {
                            $alt .= ": $post->{'picture_keyword'}";
                        }
                        $alt = LJ::ehtml($alt);

                        $$ret .= "<img align='left' hspace='3' src='$LJ::USERPIC_ROOT/$picid/$post->{'posterid'}'";
                        $$ret .= " width='$userpics{$picid}->{'width'}'";
                        $$ret .= " title='$alt' alt=''";
                        $$ret .= " height='$userpics{$picid}->{'height'}' />";
                    }
                }

                my $cleansubject = LJ::eall($post->{'subject'});

                $$ret .= "<font size='+1' face='Arial,Helvetica'><b>$cleansubject</b></font> $icon";
                $$ret .= "<br />$user\n";
                $$ret .= "<br />$datepost\n";
                if ($post->{'poster_ip'} && $remote &&
                    ($remote->{'user'} eq $up->{'user'} ||
                     LJ::check_rel($dbs, $u, $remote, 'A')))
                {
                    $$ret .= BML::ml('.fromip', {'ip'=>$post->{'poster_ip'}});
                }

                $$ret .= " <font size='-1'>(<a href='" . $talkargs->("thread=$dtid#t$dtid") . "'>$T{'link'}</a>)</font> ";

                if (LJ::Talk::can_delete($dbs, $remote, $u, $up, $userpost))
                {
                    $$ret .= "<a href='/delcomment.bml?${jargent}id=$dtid'>" . LJ::img("btn_del", "", { 'align' => 'absmiddle', 'hspace' => 2, 'vspace' => }) . "</a>";
                }

                if ($post->{'state'} eq 'A' && 
                    LJ::Talk::can_screen($dbs, $remote, $u, $up, $userpost))
                {
                    $$ret .= "<a href='/talkscreen.bml?mode=screen&amp;${jargent}talkid=$dtid'>" . LJ::img("btn_scr", "", { 'align' => 'absmiddle', 'hspace' => 2, 'vspace' => }) . "</a>";
                }

                if ($post->{'state'} eq 'S' && 
                    LJ::Talk::can_unscreen($dbs, $remote, $u, $up, $userpost))
                {
                    $$ret .= "<a href='/talkscreen.bml?mode=unscreen&amp;${jargent}talkid=$dtid'>" . LJ::img("btn_unscr", "", { 'align' => 'absmiddle', 'hspace' => 2, 'vspace' => }) . "</a>";
                }

                if ($showmultiform) {
                    $$ret .= " <nobr><input type='checkbox' name='selected_$tid' id='s$tid' />";
                    $$ret .= " <label for='s$tid'>$ML{'.select'}</label></nobr>";
                    $multiform_selects = 1;
                }
                $$ret .= "</td></tr><tr><td>";

                ## escape BML block codes.  so <?tag ... tag?> turns into (&#0061; ... tag?>
                $post->{'body'} =~ s/\(=/\(&\#0061\;/g;
                LJ::CleanHTML::clean_comment(\$post->{'body'}, $post->{'opt_preformatted'});
                BML::ebml(\$post->{'body'});
                $$ret .= $post->{'body'};

                $$ret .= "<p style='margin: 0.7em 0 0.2em 0'><font size='-2'>";
                my $replyurl = $clustered ? $talkargs->("replyto=$dtid") :
                    "$LJ::SITEROOT/talkpost.bml?${jargent}replyto=$dtid";
                $$ret .= "(<a href='$replyurl'>$T{'replythis'}</a>) ";

                if ($post->{'parenttalkid'} != 0) {
                    my $dpid =  $clustered ? ($post->{'parenttalkid'} * 256 + $init->{'anum'}) : $post->{'parenttalkid'};
                    $$ret .= "(<a href='" . $talkargs->("thread=$dpid") . "'>$T{'parent'}</a>) ";
                }
                if (defined $opts->{'children'}->{$tid}) {
                    my $url = $talkargs->("thread=$dtid");
                    $$ret .= "(<a href='$url'>$T{'thread'}</a>)";
                }
                $$ret .= "</font></p>";
                $$ret .= "</td></tr></table>\n";  # close colored table
            } else {
                # link to message

                # convert the subject to UTF-8 if needed
                if ($LJ::UNICODE && ! LJ::is_utf8($post->{'subject'})) {
                    my $error;
                    my $subj = LJ::text_convert($dbs, $post->{'subject'}, 
                                                $u, \$error);
                    if ($error) {
                        LJ::text_out(\$post->{'subject'});
                    } else {
                        $post->{'subject'} = $subj;
                    }
                }

                $$ret .= "<a name='t$dtid'></a><table><tr>";
                $$ret .= "<td><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
                $$ret .= "<td><a href='" . $talkargs->("thread=$dtid") . "'>" . BML::eall($post->{'subject'} || $T{'nosubject'}) . "</a> - $user, <i>$post->{'datepost'}</i></td></tr></table>\n";
            }
        }
    }

    if (@childs)
    {
        my $count;
        # go through children and show them too.
        foreach my $id (@childs) {
            next if ($postref->{$id}->{'_hide'});
            $self_sub->($self_sub, $postref, $ret, $id, $mark, 
                        { "depth" => $opts->{'depth'} + ($tid ? 1 : 0),
                          "children" => $opts->{'children'},
                      });
        }
    } else {
#       $$ret .= "No sub-posts.";
    }
};

 unless ($logprops{$itemid}->{'opt_nocomments'} or $u->{'opt_showtalklinks'} eq "N")
 {
     $ret .= "$navcrap" if $navcrap;
     my $readlink;
     if ($FORM{'thread'} && $pages == 1) {
         my $readurl = LJ::journal_base($u) . "/$ditemid.html";
         $readlink = "(<a href='$readurl'>$T{'readcomments'}</a>) - ";
     }

     my $posturl = "$LJ::SITEROOT/talkpost.bml?${jargent}itemid=$ditemid";
     $posturl = "$ditemid.html?mode=reply" if $new_url;

     $ret .= "<p align='center' class='lesstop'><b>$readlink(<a href='$posturl'>$T{'postcomments'}</a>)</b></p>";

     if ($post_count > 0)
     {
         $ret .= "<form style='display: inline' method='post' action='/talkmulti.bml' name='multiform'>";
         $ret .= LJ::html_hidden("ditemid", $ditemid);
         $ret .= LJ::html_hidden("journal", $u->{'user'});

         $recurse_post->($recurse_post, \%posts, \$ret, $thread, {}, { "newfirst" => 0,
                                                                       "children" => \%children,
                                                                   });

         $ret .= "<hr /><p class='lesstop' align='center'><b>$readlink(<a href='$posturl'>$T{'postcomments'}</a>)</b></p>";

         if ($showmultiform && $multiform_selects) {
             $ret .= "<p>$ML{'.talkmulti.des'} ";
             $ret .= LJ::html_select({'name' => 'mode' },
                                     "" => "",
                                     "unscreen" => $ML{'.talkmulti.unscreen'},
                                     "screen" => $ML{'.talkmulti.screen'},
                                     "delete" => $ML{'.talkmulti.delete'});
             $ret .= " " . LJ::html_submit('', $ML{'.talkmulti.submit'},
                                    { "onClick" => "return (document.multiform.mode.value != \"delete\") " .
                                                   "|| confirm(\"" . LJ::ejs($ML{'.confirm.action'}) . "\");" });
             $ret .= "</p>";
         }
         $ret .= "</form>";

     }
     if ($navcrap) {
         $ret .= "<p>$navcrap</p>";
     }
 }

 return $ret;

_code?>
<=body
windowtitle=><?_code return $_[1] ? $_[1]->{'title'} : $r_title _code?>
head=><?_code return $_[1] ? $_[1]->{'head'} : $r_head _code?>
page?><?_c <LJDEP>
link: htdocs/talkpost.bml, htdocs/talkread.bml, htdocs/delcomment.bml
img: htdocs/img/dot.gif, htdocs/img/delcomment.gif
</LJDEP> _c?>
