# -*-s2-*-

layerinfo type = "layout";
layerinfo name = "Punquin Elegant with Sidebar";
layerinfo redist_uniq = "punquin/layout";
layerinfo previews = "punquin/punquin.jpg";

property bool show_recent_userpic { des = "Show the userpic on the recent entries page?"; }
set show_recent_userpic = false;

property Color body_bgcolor { des = "Page background color"; }
set body_bgcolor = "#6666cc";

property Color main_bgcolor { des = "Background of text areas"; }
set main_bgcolor = "#ffffff";

property Color main_fgcolor { des = "Text color"; }
set main_fgcolor = "#000000";

property Color subject_color { des = "Color of entry subjects"; }
set subject_color = "#c00000";

property Color title_color { des = "Color of headers and titles"; }
set title_color = "#8b1a1a";

property Color border_color { des = "Color of borders"; }
set border_color = "#eeeeff";

property Color link_color { des = "Link color"; }
set link_color = "#000050";

property Color vlink_color { des = "Visited link color"; }
set vlink_color = "#500050";

property Color alink_color { des = "Active link color"; }
set alink_color = "#ff00c0";

property use page_recent_items;
property use text_post_comment;
property use text_read_comments;

property use page_friends_items;
property use text_post_comment_friends;
property use text_read_comments_friends;

set page_recent_items = 20;
set page_friends_items = 20;

function print_stylesheet ()
{
    print """
body {
    background:      $*body_bgcolor;
    color:           $*main_fgcolor;
    font:            normal normal 10pt verdana, arial, helvetica, sans-serif;
}
a {
    text-decoration: none;
    color:           $*link_color;
    background:      transparent;
    font:            normal normal 10pt verdana, arial, helvetica, sans-serif;
}
a:visited {
    color:           $*vlink_color;
    background:      transparent;
}
a:active {
    color:           $*alink_color;
    background:      transparent;
}
#title {
    font:            normal bold 10pt verdana, Arial, Helvetica, sans-serif;
    color:           $*title_color;
    text-align:      center;
}
    """;
} 

function Page::lay_sidebar_navigation () { }

function Page::print () 
{
    var string title = $this->title();

    var string userpic;
    var Image up_img = $.journal.default_pic;
    if (defined $up_img) {
        $userpic = """<p align="center"><img border="0" src="$up_img.url" height="$up_img.height" width="$up_img.width" alt="" /></p>""";
    }
    var string website;
    if ($.journal.website_url != "") {
        $website = """<a href="$.journal.website_url">&gt; $.journal.website_name</a><br />""";
    }

    var string links;
    $links = $links + ($.view == "recent" ? "&gt; recent&nbsp;entries<br />\n" : "&gt; <a href='$.base_url/'>recent&nbsp;entries</a><br />\n");
    $links = $links + ($.view == "friends" ? "&gt; friends<br />\n" : "&gt; <a href='$.base_url/friends/'>friends</a><br />\n");
    $links = $links + ($.view == "calendar" ? "&gt; archive<br />\n" : "&gt; <a href='$.base_url/calendar/'>archive</a><br />\n");
    
"""
<html>
<head>
<link rel="stylesheet" href="$.stylesheet_url" type="text/css"/>
<style type="text/css">
<!--
body {
    background:      $*body_bgcolor;
    color:           $*main_fgcolor;
    font:            normal normal 10pt verdana, arial, helvetica, sans-serif;
}
a {
    text-decoration: none;
    color:           $*link_color;
    background:      transparent;
    font:            normal normal 10pt verdana, arial, helvetica, sans-serif;
}
a:visited {
    color:           $*vlink_color;
    background:      transparent;
}
a:active {
    color:           $*alink_color;
    background:      transparent;
}
#title {
    font:            normal bold 10pt verdana, Arial, Helvetica, sans-serif;
    color:           $*title_color;
    text-align:      center;
}
-->
</style>
$.head_content
<title>$title</title>
</head>
<body>
<a name="top"></a>
<center>
<table border="0" cellpadding="5" cellspacing="0" width="90%">
<tr><td valign="top" align="left">
<!--links-->
<table bgcolor="$*border_color" border="0" cellpadding="5" cellspacing="0"><tr><td valign="center" align="center">
<table width=150 bgcolor="$*main_bgcolor" border="0" cellpadding="5" cellspacing="0">
<tr valign=top><td align=left>
<center>
<font face="verdana,arial,helvetica" size=2 color=%%color:page_text_title%%>
<b>$title</b>
</font>
</center>
<p>
<font face="verdana,arial,helvetica" size="2">
$links
$website
&gt;<a href="$*SITEROOT/users/$.journal.username/info"> profile</a><br />
    """;
    $this->lay_sidebar_navigation();
    """
</font>
<p>
<center>
$userpic
</center>
</td></tr></table>
</td></tr></table>
<!--links-->
</td><td valign="top" align="right" rowspan="2">
<!--journal-->
<table bgcolor="$*border_color" border="0" cellpadding="5" cellspacing="0"><tr><td valign="center" align="center">
<table width="100%" height=90% bgcolor="$*main_bgcolor" border="0" cellpadding="5" cellspacing="0">
<tr valign=top><td>
    """;
    $this->print_body();
    """
<hr noshade color="$*main_bgcolor" size="1" width="500">
</td></tr></table>
</td></tr></table>
<!--journal-->
</td></tr>
<tr><td valign="bottom" align="left">
<!--gotop-->
<table bgcolor="$*border_color" border="0" cellpadding="5" cellspacing="0"><tr><td valign="center" align="center">
<table width=150 bgcolor="$*main_bgcolor" border="0" cellpadding="5" cellspacing="0">
<tr valign=top><td align=left>
<font face="verdana,arial,helvetica" size="2">
    """;
    $this->lay_sidebar_navigation();
    """
<br /><a href="#top">&gt; top of page</a><br>
</font>
</td></tr></table>
</td></tr></table>
<!--gotop-->
</td></tr><tr><td colspan="2" align="right">
<a href="$*SITEROOT/"><font face="verdana,arial,helvetica" size="1">$*SITENAME</font></a>
</td></tr>
</table>
</center>

</body>
</html>
    """;
}

function print_entry (Page p, Entry e, Color bgcolor, Color fgcolor)
{
    var string time = $e.time->time_format();
    if ($e.new_day) {
        """<p align="right"><font color="$*title_color" face="verdana,arial,helvetica" size="3">""";
        "<b>"; print $e.time->date_format("%%month%% %%dayord%%, %%yyyy%%"); "</b>";
        """</font><hr noshade color="$*border_color" size="1" width="100%" /></p>""";
    }

    """<table border="0" cellpadding="2" cellspacing="0"><tr>""";

    if ($p.view == "friends" or
        $e.poster.username != $e.journal.username or
        $*show_recent_userpic == true)
    {
        # Lots of muddled logic. Yay.

        """<td valign=top align="middle" bgcolor="$bgcolor" width=100>
        <font face="verdana,arial,helvetica" size="2" color="$fgcolor">""";
        if ($p.view == "friends") { 
            """<a href='""" + $e.journal->base_url() + """'><b>$e.journal.username</b></a><br />""";
        }
        if ($e.poster.username != $e.journal.username) {
            if ($p.view == "friends") { 
                """[<a href='""" + $e.poster->base_url() + """'><font color='$fgcolor'>$e.poster.username</font></a>]<br />""";
            } else {
                """<a href='""" +  $e.poster->base_url() + """'><font color='$fgcolor'><b>$e.poster.username</b></font></a><br />""";
            }
        }
        if (defined $e.userpic) {
            if (($*show_recent_userpic == false) and 
	        ($p.view != "friends") and
		($p.journal_type != "C") ) { }
            else { """<img border="0" src="$e.userpic.url" width="$e.userpic.width" height="$e.userpic.height" alt="" />""";
            }
        }
        "</font></td>";
    }
    """
<td valign=top>
<font face="verdana,arial,helvetica" size="2">
<b>$time - <font color="$*title_color">
    """;
    print $e.subject; " ";
    if ($e.security != "") {
        $e.security_icon->print();
    }
    """</font></b><font face="verdana,arial,helvetica" size="2"><br />""";
    print $e.text; "<br />";
    if (size $e.metadata) {
        foreach var string k ($e.metadata) {
            var string key = $k;
            var string val = $e.metadata{$k};
            if ($k == "mood") {
                $key = $*text_meta_mood;
            } elseif ( $k == "music" ) {
	        $key = $*text_meta_music;
	    }		
            if ($k == "mood" and defined $e.mood_icon) {
                var Image i = $e.mood_icon;
                $val = "<img src='$i.url' width='$i.width' height='$i.height' align='absmiddle' alt='[mood icon]' /> $val";
            }
            "<b>$key:</b> $val<br />";
        }
    }
    """
</font>
</td></tr>
</table>
""";
    $e.comments->print(); 
}

function Page::print_entry (Entry e) 
{
    print_entry($this, $e, null Color, null Color);
}

function FriendsPage::print_entry (Entry e) {
    var Friend f = $.friends{$e.journal.username};
    print_entry($this, $e, $f.bgcolor, $f.fgcolor);
}

function RecentPage::print_body {
    foreach var Entry e ($.entries) {
        $this->print_entry($e);
    }
}

function FriendsPage::print_body {
    foreach var Entry e ($.entries) {
        $this->print_entry($e);
    }
}


function RecentPage::lay_sidebar_navigation () {
    var int total = size $.entries;
    var string nav = "";
    if ($.nav.backward_url != "") {
        $nav = """<a href="$.nav.backward_url">&gt; previous $total entries</a>""";
    }
    if ($.nav.forward_url != "" and $.nav.backward_url != "") {
        $nav = "$nav<br />";
    }
    if ($.nav.forward_url != "") {
        $nav = """$nav<a href="$.nav.forward_url">&gt; next $total entries</a>""";
    }
    print $nav;
}

function FriendsPage::lay_sidebar_navigation () {
    var int total = size $.entries;
    var string nav = "";
    if ($.nav.backward_url != "") {
        $nav = """<a href="$.nav.backward_url">&gt; previous $total entries</a>""";
    }
    if ($.nav.forward_url != "" and $.nav.backward_url != "") {
        $nav = "$nav<br />";
    }
    if ($.nav.forward_url != "") {
        $nav = """$nav<a href="$.nav.forward_url">&gt; next $total entries</a>""";
    }
    print $nav;
}

function CommentInfo::print ()
{
    """<p align="right">(""";
    if ($.count > 0 or $.screened) {
        $this->print_readlink();
        "&nbsp;|&nbsp;";
    }
    $this->print_postlink();
    ")</p>";
}

function ArchiveYearPage::print_body {
    """<p align="right"><font face="Verdana,Arial,Helvetica" color="$*title_color" size="+1"><b>$.year</b></font></p>""";
    foreach var ArchiveYearMonth m ($.months) {
        $this->print_month($m);
    }
}

function ArchiveYearPage::print_year_links ()
{
    foreach var ArchiveYearYear y ($.years) {
        if ($y.displayed) {
            "&gt; $y.year<br />";
        } else {
            "<a href=\"$y.url\">&gt; $y.year</a><br />";
        }
    }
}

function ArchiveYearPage::lay_sidebar_navigation () 
{
    $this->print_year_links();
}

function ArchiveYearPage::print_month(ArchiveYearMonth m)
{
    if (not $m.has_entries) { return; }
    """
<center><p><table border="1" cellpadding="4" width="80%">
<tr align="center"><td colspan="7" bgcolor="$*main_bgcolor">
<font face="Verdana,Arial,Helvetica" color="$*main_fgcolor"><b>
    """;
    print $m->month_format();
    """
</b></font>
</td></tr>
<tr align="center" bgcolor="$*border_color">
    """;
    foreach var int d ($.weekdays) {
        "<td><font face='Verdana,Arial,Helvetica'>"+$*lang_dayname_short[$d]+"</font></td>\n";
    }
    "</tr>";
    foreach var ArchiveYearWeek w ($m.weeks) {
        $w->print();
    }
    """
<tr align="center"><td colspan="7"><a href="$m.url">view subjects</a>
</td></tr>
</table>
</center>
    """;
}

function ArchiveYearWeek::print () {
    "<tr>";
    if ($.pre_empty) { "<td colspan='$.pre_empty' bgcolor='$*border_color'>&nbsp;</td>"; }
    foreach var ArchiveYearDay d ($.days) {
        """<td valign="top"><b><font face="Verdana,Arial,Helvetica" size="-1">$d.day</font></b><div align="center">""";
        if ($d.num_entries) {
            """<a href="$d.url">$d.num_entries</a>""";
        } else {
            "&nbsp;";
        }
        "</div></td>";
    }
    if ($.post_empty) { "<td colspan='$.post_empty' bgcolor='$*border_color'>&nbsp;</td>"; }
    "</tr>";
}

function DayPage::print_body() {
    if (not $.has_entries) {
        """<table width="100%"><tr><td align="RIGHT"><p align="right">
        <font color="$*title_color" face="verdana,arial,helvetica" size="3">""";
        "<b>"; print $.date->date_format("%%month%% %%dayord%%, %%yyyy%%"); "</b>";
        """</font></p><hr noshade color="$*border_color" size="1" width="100%" /></td></tr><tr><td>
        <br /><br /><br /><center><font face="Verdana,Arial,Helvetica" size="1">""";
        print ehtml($*text_noentries_day);
        "</center></font><br /><br /><br /></td></tr></table>";
    } else {
        foreach var Entry e ($.entries) {
            $this->print_entry($e);
        }
    }

    var string tprev = ehtml($*text_day_prev);
    var string tnext = ehtml($*text_day_next);

    """
<hr noshade color="$*border_color" size="1" width="100%" />
<table width="100%">
<tr align=middle>
<td width=33% align=left><font face="verdana,arial,helvetica" size="1">
<a href="$.prev_url">$tprev</a>
</font></td>
<td align=center width=33%><font face="verdana,arial,helvetica" size="1">
[<a href="$*SITEROOT/users/$.journal.username/calendar">calendar</a>]
</font></td>
<td width=33% align=right><font face="verdana,arial,helvetica" size="1">
<a href="$.next_url">$tnext</a>
</font>
</td>
</tr>
</table>
    """;
}
