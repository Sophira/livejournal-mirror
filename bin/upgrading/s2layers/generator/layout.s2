# -*-s2-*-

layerinfo type = "layout";
layerinfo name = "Generator";
layerinfo redist_uniq = "generator/layout";
layerinfo previews = "generator/generator.jpg";

property Color entry_back { des = "Entry background"; }
set entry_back = "#ffffff";

property Color entry_text { des = "Entry text color"; }
set entry_text = "#000000";

property Color page_link { des = "Link color"; }
set page_link = "#0000ff";

property Color page_vlink { des = "Visited link color"; }
set page_vlink = "#0000ff";

property Color page_alink { des = "Active link color"; }
set page_alink = "#00ffff";

property Color page_back { des = "Page background color (and around userpics)"; }
set page_back = "#2d4f89";

property Color strong_text { des = "Text on strong surrounding color (where needed)"; }
set strong_text = "#000000";

property Color stronger_back { des = "Background color for the bar above entries"; }
set stronger_back = "#000000";

property Color stronger_text { des = "Text color for the bar above entries"; }
set stronger_text = "#ffffff";

property Color weak_back { des = "Background color for the bar below entries"; }
set weak_back = "#aaaaaa";

property Color weak_text { des = "Text color for the bar below entries"; }
set weak_text = "#000000";

property use text_post_comment;
property use text_read_comments;

property use text_post_comment_friends;
property use text_read_comments_friends;

set text_post_comment = "post comment";
set text_post_comment_friends = "post comment";

function print_stylesheet () {
    " /// stylesheeet!!!\n";
}

function Page::lay_top_userpic () { 
    var Image up_img = $.journal.default_pic;
    if (defined $up_img) {
        """<td><table cellpadding="2" bgcolor="$*entry_back" cellspacing="0" border="0" summary="">
<tr><td bgcolor="$*stronger_back" align="center"><img border="0" src="$up_img.url" height="$up_img.height" width="$up_img.width" alt=""></td></tr>
</table></td>
""";
    }
}
function FriendsPage::lay_top_userpic () { }

function Page::print () 
{
    var string title = $this->title();

    var string website;
    if ($.journal.website_url != "") {
        $website = """<tr><td class="meta">[</td>
<td class="meta" align="right"><b>website</b></td>
<td class="meta" align="center">|</td>
<td class="meta"><a href="$.journal.website_url">$.journal.website_name</a></td>
<td class="meta">]</td></tr>""";
    }

    var string links;
    $links = $links + ($.view == "recent" ? "<b>entries</b>" : "<a class='index' href='$.base_url/'>entries</a>") + "|";
    $links = $links + ($.view == "friends" ? "<b>friends</b>" : "<a class='index' href='$.base_url/friends'>friends</a>") + "|";
    $links = $links + ($.view == "archive" ? "<b>archive</b>" : "<a class='index' href='$.base_url/calendar'>archive</a>");
    
"""<!doctype HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<html><head>
<style type="text/css">
body, td {
font-family: verdana, sans-serif;
font-size: 10pt;
}
tt, pre {
font-family: monospace;
}
a {
text-decoration: none;
}
a:hover {
text-decoration: underline;
}
.shadowed {
font-size: 8pt;
background: $*weak_back;
}
.meta {
font-size: 8pt;
}
.index {
font-size: 8pt;
}
.caption, .index {
color: $*stronger_text;
}
.comments {
font-size: 8pt;
}
</style>
$.head_content
<title>$title</title>
</head>
<body bgcolor="$*page_back" text="$*entry_text" link="$*page_link" vlink="$*page_vlink" alink="$*page_alink">
<div align="center">
<table width="600" cellpadding="2" cellspacing="0"
border="0"
summary="">
<tr><td bgcolor="$*stronger_back" align="center">
<table width="100%" cellpadding="3" cellspacing="0"
border="0"
summary="">
<tr class="caption">
<td class="caption"><b>$title</b></td>
<td class="index" align="right">[$links]</td>
</tr></table>
<table width="100%" cellpadding="3" cellspacing="0"
border="0"
summary="">
<tr>
<td class="shadowed" align="center">
<table cellspacing="5"
cellpadding="0"
border="0"
summary="" style="margin-left: auto; margin-right: auto;">
<tr>""";
    $this->lay_top_userpic();

"""<td>
<div align="center"><b>$.journal.name</b></div><p>
<table cellspacing="0"
cellpadding="0"
border="0"
summary="">
$website
<tr>
<td class="meta">[</td>
<td class="meta" align="right"><b>userinfo</b></td>
<td class="meta" align="center">|</td>
<td class="meta"><a href="$.view_url{"userinfo"}">livejournal userinfo</a></td>
<td class="meta">]</td>
</tr>
<tr>
<td class="meta">[</td>
<td class="meta" align="right"><b>archive</b></td>
<td class="meta" align="center">|</td>
<td class="meta"><a href="$.view_url{"archive"}">journal archive</a></td>
<td class="meta">]</td>
</tr>
</table>
</td>
</tr>
</table>
</td>
</tr></table>
</td></tr>
</table>
<p>
""";
    $this->print_body();

"""
</div>
<p>
</body>
</html>
""";
}

function print_entry (Page p, Entry e, Color bgcolor, Color fgcolor)
{
    var string datetime;
    $datetime = $e.time->date_format("med")+"|<b>" 
        + $e.time->time_format() + "</b>";

    """
<table width="600" cellpadding="2" cellspacing="0" border="0" summary="" class="entrybox">
<tr align='left'>
<td bgcolor="$*stronger_back" align="center">
<table width="100%" cellpadding="5" cellspacing="0" border="0" summary="">
<tr align='left'>
<td class="caption">""";
    
    if ($e.security != "") {
        $e.security_icon->print();
    }
    
    """ $e.subject</td>
<td align="right" class="index">[$datetime]</td>
</tr>
<tr align='left'>
<td colspan="2" bgcolor="$*entry_back">
""";

    if ($p.view == "friends" or
        $p.journal_type == "C" or
        $e.poster.username != $e.journal.username) 
    {
        """<table cellpadding="1" align="right" cellspacing="0" border="0" summary=""><tr align='left'><td bgcolor="$*stronger_back">""";
        """<table cellpadding="2" align="center" cellspacing="0" border="0" summary="">""";
        """<tr align='left'><td bgcolor="$bgcolor" align="center"><a class="index" href=\"""" + $e.journal->base_url() + "\">";
        if (defined $e.userpic) {
            """<img border="0" src="$e.userpic.url" width="$e.userpic.width" height="$e.userpic.height" alt=""><br>""";
        }

        "<font color=\"$fgcolor\">$e.journal.username</font></a>";
        if ($e.poster.username != $e.journal.username) {
            "<br>[<a class=\"index\" href=\"" +
                $e.poster->base_url() + "\"><font color=\"$fgcolor\">$e.poster.username</font></a>]";
        }
        "</td></tr></table></td></tr></table>";
    }

    if (size $e.metadata) {
        """<table cellspacing="0" cellpadding="0" border="0" summary="">""";
        foreach var string k ($e.metadata) {
            var string text = $k;
            var string val = $e.metadata{$k};
            if ($k == "mood") {
                $text = $*text_meta_mood;
            } elseif ($k == "music") {
                $text = $*text_meta_music;
            }
            if ($k == "mood" and defined $e.mood_icon) {
                var Image i = $e.mood_icon;
                $val = "<img src='$i.url' width='$i.width' height='$i.height' align='absmiddle'> $val";
            }
            """<tr><td class="meta">[</td><td class="meta" align="right"><b>$text</b></td>""";
            """<td class="meta" align="center">|</td><td class="meta">$val</td><td class="meta">]</td></tr>""";
        }
        """</table><p>""";
    }

"""
$e.text
</td>
</tr>
<tr bgcolor="$*weak_back"><td align='left' class='comments'>
<a href="$e.permalink_url">$*text_permalink</a>
</td><td align='right' class='comments'>
""";

    $e.comments->print();

"""
</td></tr>
</table>
</td>
</tr>
</table><p>
""";

} # print_entry(Page,Entry,Color,Color)

function Page::print_entry (Entry e) {
   print_entry($this, $e, null Color, null Color);
}

function FriendsPage::print_entry (Entry e) {
   var Friend f = $.friends{$e.journal.username};
   print_entry($this, $e, $f.bgcolor, $f.fgcolor);
}

function RecentPage::print_body ()
{
    foreach var Entry e ($.entries) {
        $this->print_entry($e);
    }

    var string range = "most recent entries";
    if ($.nav.skip > 0) {
        $range = "$.nav.skip entries back";
    }

"""
<table cellpadding="2" cellspacing="0"
border="0"
summary="">
<tr><td bgcolor="$*stronger_back">
<table cellpadding="3" cellspacing="0"
border="0"
summary="">
<tr>
<td align="center" class="index">navigation</td>
</tr>
<tr>
<td bgcolor="$*entry_back" align="center">
<table cellspacing="0"
cellpadding="0"
border="0"
summary="">
<tr>
<td class="meta">[</td>
<td class="meta" align="right"><b>viewing</b></td>
<td class="meta" align="center">|</td>
<td class="meta">$range</td>
<td class="meta">]</td>
</tr>
""";

    # go forward/backward if possible
    if ($.nav.forward_url != "" or $.nav.backward_url != "") {
        var string sep;
        var string back;
        var string forward;
        if ($.nav.backward_url != "") {
            $back = """<a href="$.nav.backward_url">earlier</a>""";
        }
        if ($.nav.forward_url != "") {
            $forward = """<a href="$.nav.forward_url">later</a>""";
        }
        if ($back != "" and $forward != "") { $sep = "/"; }
        """<tr>
<td class="meta">[</td>
<td class="meta" align="right"><b>go</b></td>
<td class="meta" align="center">|</td>
<td class="meta">$back$sep$forward</td>
<td class="meta">]</td>
</tr>""";
    }

    "</table></td></tr></table></table>";
}

function CommentInfo::print ()
{
    if (not $.enabled) { return; }
    if ($.count > 0 or $.screened) {
        "<b>"; $this->print_readlink(); "</b>";
        "|";
    }
    $this->print_postlink();
}

function ArchiveYearPage::print_year_links ()
{
    """<table cellpadding="2" cellspacing="0" border="0" summary="">
<tr><td bgcolor="$*stronger_back" align="center">
<table cellpadding="5" cellspacing="0" border="0" summary="">
<tr><td class="caption">Years</td></tr><tr>
<td colspan="2" bgcolor="$*entry_back">""";
    foreach var ArchiveYearYear y ($.years) {
        if ($y.displayed) {
            "<b>$y.year</b>&nbsp;";
        } else {
            "<a href=\"$y.url\">$y.year</a>&nbsp;";
        }
    }
   """</td></tr></table></td></tr></table><p>""";

}

function ArchiveYearPage::print_month (ArchiveYearMonth m)
{       
    if (not $m.has_entries) { return; }
    """<table cellpadding="2" cellspacing="0" border="0" summary="">
<tr><td bgcolor="$*stronger_back" align="center">
<table cellpadding="5" cellspacing="0" border="0" summary="">
<tr>
<td class="caption">""";
        print $m->month_format();
        """</td> 
<td class="caption" align="right">[<a href="$m.url" class="index">subjects</a>]</td>
</tr>
<tr>
<td colspan="2" bgcolor="$*entry_back">
<!-- now the headings for the week -->
<table width="100%" cellpadding="5" cellspacing="0" border="0" summary="">
<tr align="center">
""";
    foreach var int d ($.weekdays) {
        "<td>"+$*lang_dayname_short[$d]+"</td>\n";
    }
    
    "</tr>";

    foreach var ArchiveYearWeek w ($m.weeks) {
        $w->print();
    }

    """</table></td></tr></table></td></tr></table><p>""";
}

function ArchiveYearWeek::print () {
    "<tr valign='top'>";
    if ($.pre_empty) { "<td colspan='$.pre_empty'></td>"; }
    foreach var ArchiveYearDay d ($.days) {
        """<td><div class="meta">$d.day</div>""";
        if ($d.num_entries) {
            """<div align="center"><a href="$d.url">$d.num_entries</a></div>""";
        } else {
            "&nbsp;";
        }
        "</td>";
    }
    if ($.post_empty) { "<td colspan='$.post_empty'></td>"; }
    "</tr>";
}

function DayPage::print_body() {

    """<table cellpadding="2" cellspacing="0" border="0" summary="">
<tr><td bgcolor="$*stronger_back" align="center">
<table cellpadding="5" cellspacing="0" border="0" summary="">
<tr><td bgcolor="$*entry_back">""";

    if ($.has_entries) {
        print $.date->date_format("long");
    } else {
        print ehtml($*text_noentries_day);
    }

    """</td></tr></table></td></tr></table><p>""";

    foreach var Entry e ($.entries) {
        $this->print_entry($e);
    }

    var string tprev = ehtml($*text_day_prev);
    var string tnext = ehtml($*text_day_next);
    var string daylong = $.date->date_format("long");

"""<table cellpadding="2" cellspacing="0" border="0" summary="">
<tr><td bgcolor="$*stronger_back">
<table cellpadding="3" cellspacing="0" border="0" summary="">
<tr>
<td align="center" class="index">navigation</td>
</tr>
<tr>
<td bgcolor="$*entry_back" align="center">
<table cellspacing="0" cellpadding="0" border="0" summary="">
<tr>
<td class="meta">[</td>
<td class="meta" align="right"><b>viewing</b></td>
<td class="meta" align="center">|</td>
<td class="meta">$daylong</td>
<td class="meta">]</td>
</tr>

<tr>
<td class="meta">[</td>
<td class="meta" align="right"><b>go</b></td>
<td class="meta" align="center">|</td>
<td class="meta"><a href="$.prev_url">$tprev</a>|<a href="$.next_url">$tnext</a></td>
<td class="meta">]</td>
</tr>
</table>
</td>
</tr>
</table>
</table>""";

}


function print_theme_preview () {
  """<table width='100%' bgcolor='$*page_back' cellpadding=10><tr><td align='center'>

<table width='400' cellpadding='2' cellspacing='2'>
<tr align='left'><td bgcolor="$*stronger_back" align="center">

<table width="100%" cellpadding="5" cellspacing="0" border="0" summary="">
<tr align='left'><td style="color: $*stronger_text">Preview Subject</td><td align="right" style="font-size: 8pt; color: $*stronger_text">[Feb. 5th, 2002|<b>8:46 pm</b>]</td></tr>
<tr align='left'>
<td colspan="2" bgcolor="$*entry_back" style='color: $*entry_text'>
Preview text, preview text, etc, etc..... words, words and more words.
</td></tr>

<tr bgcolor="$*weak_back"><td align='left' class='style: font-size: 8pt'>
<a style='text-decoration:none;color: $*page_link' href="#">$*text_permalink</a>
</td><td align='right' style='font-size: 8pt'>
<a href='#' style='text-decoration:none;color:$*page_vlink'><b>1 comment</b></a>|<a href='#' style='text-decoration:none;color:$*page_link'>post comment</a>

</td></tr></table>
</td></tr></table>
</td></tr></table>
""";
}
