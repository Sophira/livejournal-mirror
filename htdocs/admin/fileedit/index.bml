<?_code
    use LJ;

    my ($ret, $sth);

    my $DEF_ROW = 30;
    my $DEF_COL = 80;

    my $remote = LJ::get_remote();

    my %files = ();  # keys: files remote user has access to, value: 1
    my $INC_DIR = $LJ::BML_INC_DIR_ADMIN || $LJ::BML_INC_DIR || "$LJ::HTDOCS/inc";

    unless (LJ::remote_has_priv($remote, "fileedit", \%files)) {
        return "You don't have access to edit any files, or you're not logged in.";
    }

    my $valid_filename = sub {
        my $filename = shift;
        return unless $filename;
        return if $filename =~ /^\./;
        return ($filename =~ /^[a-zA-Z0-9-\_\.]{1,255}$/);
    };

    if ($files{'*'}) {
        # if user has access to edit all files, find what those files are!
        delete $files{'*'};
        opendir (DIR, $INC_DIR);
        while (my $file = readdir(DIR)) {
            $files{$file} = 1;
        }
        closedir (DIR);
    }

    ## get rid of files that don't match our safe pattern
    {
        my @del;

        foreach my $k (keys %files) {
            unless ($valid_filename->($k)) {
                push @del, $k;
            }
        }

        foreach my $k (@del) {
            delete $files{$k};
        }
    }

    my $mode = $FORM{'mode'};

    unless ($mode) {
        $mode = $FORM{'file'} ? "edit" : "pick";
    }

    if ($mode eq "pick") {
        $ret .= "<FORM METHOD=GET>\n";
        $ret .= "Pick file to edit: <SELECT NAME=\"file\">";

        foreach my $file (sort keys %files) {
            $ret .= "<OPTION VALUE=\"$file\">$file\n";
        }

        $ret .= "</SELECT> <INPUT TYPE=SUBMIT VALUE=\"load...\"><BR>";
        $ret .= "Wordwrap? <INPUT TYPE=CHECKBOX VALUE=1 NAME=w> ";
        $ret .= "Rows: <INPUT SIZE=3 NAME=r VALUE=$DEF_ROW> ";
        $ret .= "Cols: <INPUT SIZE=3 NAME=c VALUE=$DEF_COL> ";
        $ret .= "</FORM>";

        return $ret;
    }

    my $file = $FORM{'file'};

    unless ($files{$file}) {
        return "<B>ERROR!</B> you don't have access to this document.";
    }

    if ($mode eq "edit")  {
        my $contents = LJ::load_include($file);

        unless (defined $contents) {
            return "<B>Error:</B> Couldn't open file";
        }

        my $r = ($FORM{'r'}+0) || $DEF_ROW;
        my $c = ($FORM{'c'}+0) || $DEF_COL;
        my $wrap = $FORM{'w'} ? "SOFT" : "OFF";

        $ret .= "<B>Editing:</B> <tt>$file</tt><P>";
        $ret .= "<FORM METHOD=POST>\n";
        $ret .= LJ::form_auth();
        $ret .= "<INPUT TYPE=HIDDEN NAME=mode VALUE=\"save\">";
        $ret .= "<INPUT TYPE=HIDDEN NAME=file VALUE=\"$file\">";
        $ret .= "<TEXTAREA ROWS=$r COLS=$c WRAP=$wrap NAME=contents>";
        $ret .= BML::eall($contents);
        $ret .= "</TEXTAREA><P><INPUT TYPE=SUBMIT VALUE=\"Save\"> (no undo.. are you sure?)";
        $ret .= "</FORM>\n";

        return $ret;
    }

    if ($mode eq "save")  {
        unless (LJ::did_post()) {
            return "<b>$ML{'Error'}</b> requires post";
        }

        unless (LJ::check_form_auth()) {
            return "<b>$ML{'Error'}</b> $ML{'error.invalidform'}";
        }

        $ret .= "<B>Saving:</B> <tt>$file</tt><p>";

        if (LJ::save_include($file, $FORM{'contents'})) {
            $ret .= "saved.";
        } else {
             $ret .= "<b>Error saving</b>";
        }
        return $ret;
    }

    return "unknown mode";

_code?><?_c <LJDEP>
form: htdocs/admin/fileedit/index.bml
post: htdocs/admin/fileedit/index.bml
</LJDEP> _c?>
