<?_info
nocache=>1
_info?><?page
title=>Reset Account Password

body<=
<?_code
{
    use strict;
    use vars qw(%GET);

    # get the auth action
    my $qs = $GET{auth};
    return BML::redirect("$LJ::SITEROOT/lostinfo.bml")
        unless $qs;

    # now validate the argument
    return LJ::bad_input("Invalid argument given")
        unless $qs && $qs =~ /^(\d+)\.(.+)$/;

    # get the auth action info
    my ($aaid, $auth) = ($1, $2);
    my $aa = LJ::is_valid_authaction($aaid, $auth);
    return LJ::bad_input("Invalid argument given")
        unless $aa;
    return LJ::bad_input("This action has already been performed")
        if $aa->{'used'} eq 'Y';
    return LJ::bad_input("Invalid action")
        unless $aa->{action} eq 'reset_password';

    # get the user we're resetting
    my $u = LJ::load_userid($aa->{'userid'});
    return LJ::bad_input("Internal error: invalid user") unless $u;

    # do some maintenance
    my $oldval = Digest::MD5::md5_hex($u->{'password'} . "change");
    LJ::infohistory_add($u, 'password', $oldval);
    $u->log_event('password_reset', { remote => LJ::get_remote() });

    # forced password change maintenance
    unless ($LJ::DISABLED{'force_pass_change'}) {
        $u->set_prop('badpassword', 0);
    }

    # kill all sessions, forcing user to relogin
    $u->kill_all_sessions;

    # now generate a new password for them
    my $password = LJ::make_auth_code(8);
    LJ::update_user($u, { password => $password });

    # now email it to them
    my $text = <<EOF;
This is to inform you that the password to your $LJ::SITENAME account
has been reset.  Here is the new information to log in to your account:

    Account:  $u->{user}
    Password: $password

You can now change your password to something you will remember here:

    $LJ::SITEROOT/changepassword.bml


Thank you,
The $LJ::SITENAME Team
EOF

    # send it
    LJ::send_mail({
        to => $aa->{arg1}, # email address we sent their link to
        from => $LJ::ADMIN_EMAIL,
        fromname => $LJ::SITENAME,
        subject => "$LJ::SITENAME Password Reset",
        body => $text,
    });

    # and let them know
    return "<?h1 Password Reset Confirmation h1?>" .
           "<?p Your password has been reset and a new password has been emailed to you. p?>";
}
_code?>

<=body
page?>
