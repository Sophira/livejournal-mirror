<?page
title=>Append Request
body<=

<?_code

 my $status = "";

 my $spid = $FORM{'spid'}+0;
 my $sp = LJ::Support::load_request($spid);
 unless ($sp) { return "<?h1 Error h1?><?p Unknown support request. p?>"; }

 if ($sp->{'state'} eq "closed") {
     return "<?h1 Closed h1?><?p This support request has since been closed. p?>";
 }

 my $u;
 if ($sp->{'reqtype'} eq "user") {
     $u = LJ::load_userid($sp->{'requserid'});
 }

 my $remote = LJ::get_remote();
 LJ::Support::init_remote($remote);

 unless (LJ::Support::can_append($sp, $remote, $FORM{'auth'}) || $remote) {
     return LJ::bad_input("You must <a href=\"/login.bml\">login</a> to help people out.");
 }

 my $scat = $sp->{_cat};
 my $problemarea = $scat->{'catname'};
 my $catkey = $scat->{'catkey'};
 
 unless ($FORM{'spid'}) { return LJ::bad_input("need spid"); }
 unless (LJ::did_post) { return LJ::bad_input("<?requirepost?>"); }

 ### insert record
 my $faqid = $FORM{'faqid'}+0;
 
 my %answer_types = LJ::Support::get_answer_types($sp, $remote, $FORM{'auth'});

 my $type = $FORM{'replytype'};
 unless (defined $answer_types{$type}) {
     return LJ::bad_input("Invalid reply type.");
 }

 ## can we do the action we want?
 if ($FORM{'approveans'} && 
     ($type ne "internal" || ! LJ::Support::can_help($sp, $remote)))
 {
     return LJ::bad_input("To approve an answer, you must select \"Internal Comment ".
                       "/ Action\" and optionally explain why you're approving it.");
 }

 if ($FORM{'changecat'} &&
     ($type ne "internal" || ! LJ::Support::can_perform_actions($sp, $remote)))
 {
     return LJ::bad_input("To change a request's category, you must select \"Internal Comment ".
                       "/ Action\" and explain why you're changing it.");
 }

 if (($FORM{'touch'} || $FORM{'untouch'}) &&
     ($type ne "internal" || ! LJ::Support::can_perform_actions($sp, $remote)))
 {
     return LJ::bad_input("To touch this request, you must select \"Internal Comment ".
                       "/ Action\" and explain why you're touching it.");
 }

 if ($FORM{'changesum'} && ($type ne "internal" ||
     ! LJ::Support::can_help($sp, $remote)))
 {
     return LJ::bad_input("To change the summary of the request, you must select \"Internal Comment".
                       "/ Action\" and enter in the new summary.");
 }

 unless ($FORM{'body'} =~ /\S/ || $FORM{'faqid'} || 
         ($type eq "internal" && $FORM{'approveans'})) {
     return LJ::bad_input("Your message was blank.  Please type at least something in the message field.");
 }

 ## touch/untouch request
 if ($FORM{'touch'}) {
     my $dbh = LJ::get_db_writer();
     $dbh->do("UPDATE support SET state='open', timetouched=UNIX_TIMESTAMP(), timeclosed=0 WHERE spid=$spid");
     $status .= "(Inserting request into queue)\n\n";
 }
 if ($FORM{'untouch'}) {
     my $dbh = LJ::get_db_writer();
     $dbh->do("UPDATE support SET timelasthelp=UNIX_TIMESTAMP() WHERE spid=$spid");
     $status .= "(Removing request from queue)\n\n";
 }

 if (LJ::Support::is_poster($sp, $remote, $FORM{'auth'})) 
 {
     my $dbh = LJ::get_db_writer();
     $dbh->do("UPDATE support SET state='open', timetouched=UNIX_TIMESTAMP(), timeclosed=0 WHERE spid=$spid");
 }

 ## change category
 if ($FORM{'changecat'}) {
     my $newcat = $FORM{'changecat'}+0;
     my $cats = LJ::Support::load_cats($newcat);
     if ($cats->{$newcat}) {
         my $dbh = LJ::get_db_writer();
         $dbh->do("UPDATE support SET spcatid=$newcat WHERE spid=$spid");
         $status .= "Changing from $catkey => $cats->{$newcat}->{'catkey'}\n\n";
         $sp->{'spcatid'} = $newcat; # update category so IC e-mail goes to right place
     } else {
         return LJ::bad_input("That category doesn't seem to exist.");
     }
 }

 ## approving a screened entry
 if ($FORM{'approveans'}) {
     my $splid = $FORM{'approveans'}+0;
     my $res = LJ::Support::load_response($splid);
     if ($res->{'spid'} == $spid && $res->{'type'} eq "screened") {
         return LJ::bad_input('Invalid type to approve screened response as.')
            if (($FORM{'approveas'} ne 'answer') && ($FORM{'approveas'} ne 'comment'));
         # approve
         my $dbh = LJ::get_db_writer();
         my $qtype = $dbh->quote($FORM{'approveas'});
         $dbh->do("UPDATE supportlog SET type=$qtype WHERE splid=$splid");
         $status .= "(Approving $FORM{'approveas'} \#$splid)\n\n";
         LJ::Support::mail_response_to_user($sp, $splid);
     } else {
         return LJ::bad_input("Invalid screened response to approve.");
     }
 }

 ## change summary
 if ($FORM{'changesum'}) {
     $FORM{'body'} =~ s/[\n\r]//g;
     my $dbh = LJ::get_db_writer();
     my $qnewsub = $dbh->quote($FORM{'body'});
     $dbh->do("UPDATE support SET subject=$qnewsub WHERE spid=$spid");
     $status .= "Changing subject from \"$sp->{'subject'}\" to: ";
 }

 my $splid = LJ::Support::append_request($sp, {
     'body' => $status . $FORM{'body'},
     'posterid' => $remote ? $remote->{'userid'} : 0,
     'type' => $type,
     'faqid' => $faqid,
 });

 LJ::Support::mail_response_to_user($sp, $splid);

 my $auth_arg = $FORM{'auth'} ? "&auth=$FORM{'auth'}" : "";
 return "<?h1 Logged h1?><?p Your comment/solution <a href=\"see_request.bml?id=$spid$auth_arg\">has been recorded</a>. p?>
    <ul>
    <li>Go back to <a href='see_request.bml?id=$sp->{'spid'}'>Request #$sp->{'spid'}</a></li>
    <li>Go to <a href='see_request.bml?id=$sp->{'spid'}&amp;find=prev'>previous</a> /
              <a href='see_request.bml?id=$sp->{'spid'}&amp;find=next'>next</a> open request</li>
    <li>Go to <a href='see_request.bml?id=$sp->{'spid'}&amp;find=cprev'>previous</a> /
              <a href='see_request.bml?id=$sp->{'spid'}&amp;find=cnext'>next</a> open request in the same category</li>
    </ul>";
_code?>
<?hr?>
Back to the <A HREF="help.bml">list of open requests</A>.<BR>
Back to the <A HREF="./">support area</A>.

<=body
page?><?_c <LJDEP>
link: htdocs/login.bml, htdocs/support/see_request.bml, htdocs/support/help.bml
link: htdocs/support/index.bml
</LJDEP> _c?>
