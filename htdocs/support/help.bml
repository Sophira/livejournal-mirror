<?page
title=>Support Requests
body<=

<?_code
 use strict;
 use vars qw(%FORM);

 LJ::set_active_crumb('supporthelp');

 my ($ret, $sth);
 my $r = Apache->request;

 my $remote = LJ::get_remote();
 LJ::Support::init_remote($remote);

 return $ML{'.interim'} if
    (!$remote || !$remote->{'_priv'}) && 
    $r->header_in("Referer") eq "$LJ::SITEROOT/support/";

 my $cats = LJ::Support::load_cats();

 my $filtercat = $FORM{'cat'};
 $filtercat = "" unless ($filtercat =~ /^[\w\-]+$/);
 my $filterwhere;

 if ($filtercat eq "_nonpublic") {
     $filterwhere = " AND s.spcatid IN (0";
     foreach my $cat (values %$cats) {
         if (! $cat->{'public_read'} && LJ::Support::can_read_cat($cat, $remote)) {
             $filterwhere .= ", $cat->{'spcatid'}";
         }
     }
     $filterwhere .= ")";
 } elsif ($filtercat eq "_nonprivate") {
     $filterwhere = " AND s.spcatid IN (0";
     foreach my $cat (values %$cats) {
         if ($cat->{'public_read'}) {
             $filterwhere .= ", $cat->{'spcatid'}";
         }
     }
     $filterwhere .= ")";
 } else {
     my $fcat = LJ::Support::get_cat_by_key($cats, $filtercat);
     if (LJ::Support::can_read_cat($fcat, $remote)) {
         $filterwhere = " AND s.spcatid=$fcat->{'spcatid'}";
     } else {
         $filtercat = "";
     }
 }

 my $total_open;
 my $dbr = LJ::get_db_reader();
 my $append;
 if ($FORM{'state'} eq "closed")
 {
     $ret .= "<?h1 Recently Closed Support Requests h1?><?p Below are all support requests that have been recently closed.  To return to the open requests, <a href=\"./help.bml?cat=$filtercat\">click here</a>. p?>";
     $sth = $dbr->prepare("SELECT s.* FROM support s WHERE s.state='closed' AND s.timeclosed>UNIX_TIMESTAMP()-(3600*24) $filterwhere");
 }
 elsif ($FORM{'state'} eq "youreplied") 
 {
     unless ($remote) {
         return "<?h1 Error h1?><?p You must be logged in to filter on requests that you've replied to. p?>";
     }
     $ret .= "<?h1 Requests you replied to h1?><?p This shows all open requests you've replied to. p?>";
     $sth = $dbr->prepare("SELECT s.* FROM support s, supportlog sl
                           WHERE s.spid=sl.spid AND sl.userid=$remote->{'userid'} $filterwhere
                           AND (s.state='open' OR (s.state='closed' AND s.timeclosed>UNIX_TIMESTAMP()-(3600*24)))");
 }
 else 
 {
     $ret .= "<?h1 Open Support Requests h1?><?p Below are all support requests that are open (they just came in and haven't been touched yet) or answered (either awaiting to be closed by the person needing help, or the person requested they still need help).  The <a href=\"./help.bml?state=closed&amp;cat=$filtercat\">closed reports</a> are also available.  If you help somebody out and they confirm you helped them, you get the number of points indicated in the status column.  These points will show up on your userinfo page. p?>";

     $append = 1;
     $sth = $dbr->prepare("SELECT s.* FROM support s WHERE s.state='open' $filterwhere");
 }

 $sth->execute;
 
 # For the You Replied filter, we might be getting some rows multiple times (when
 # multiple log rows exist for $remote), which is still better than using DISTINCT
 # in the query which uses a temporary table, so ensure uniqueness here.

 my @support_log;
 my %spids_seen;
 my $rct = 0;
 while (my $sprow = $sth->fetchrow_hashref) {
    next if $spids_seen{$sprow->{'spid'}};
    $spids_seen{$sprow->{'spid'}} = 1;
    push @support_log, $sprow;
    $rct++;
 }
 $ret .= "<p>[<b>$rct</b> total open requests]</p>" if ($append);
 @support_log = sort { $b->{timecreate} <=> $a->{timecreate} } @support_log;

 # filter line:
 $ret .= "<form method='get' action='help.bml'>Show only ";
 $ret .= "<select name='state'>";
 {
     my @states = ("" => "Open",
                   "closed" => "Closed",
                   "green" => "Green");
     if ($remote) {
         push @states, ("youreplied", "You Replied");
     }
     while (@states) {
         my ($skey, $sname) = splice(@states, 0, 2);
         my $sel = $FORM{'state'} eq $skey ? " selected='selected'" : "";
         $ret .= "<option value=\"$skey\"$sel>$sname</option>";
     }
 }
 $ret .= "</select>";

 $ret .= " requests of type: <select name='cat'>";
 $ret .= "<option value=\"\">(All)</option>";
 my @filter_cats = LJ::Support::filter_cats($remote, $cats);
 if (LJ::check_priv($remote, "supportread")) {
     unshift @filter_cats, { 'catkey' => '_nonpublic',
                             'catname' => '(Private)' };
     unshift @filter_cats, { 'catkey' => '_nonprivate',
                             'catname' => '(Public)' };
 }
 foreach my $cat (@filter_cats)
 {
     my $sel = $filtercat eq $cat->{'catkey'} ? " selected='selected'" : "";
     $ret .= "<option value=\"$cat->{'catkey'}\"$sel>$cat->{'catname'}</option>";
 }
 $ret .= "</select>\n";
 $ret .= "<input type=submit value=\"Filter\" /></form>";
 # /filter line
 

 $ret .= "<p><table cellpadding='4' cellspacing='1' border='1' bgcolor='#ffffff'><tr bgcolor='#d0d0d0'>\n";
 $ret .= "<td><b>ID#</b></td>\n";
 $ret .= "<td><b>Summary</b></td>\n";
 $ret .= "<td><b>Problem Area</b></td>\n";
 $ret .= "<td><b>Posted</b></td>\n";
 $ret .= "<td><b>Status</b></td>\n";
 $ret .= "</tr>";

 foreach my $sp (@support_log)
 {
     LJ::Support::fill_request_with_cat($sp, $cats);
     next unless (LJ::Support::can_read($sp, $remote));

     my $status = "open";
     my $barbg = "bgcolor='#d0eed0'";
     if ($sp->{'timeclosed'}) {
         $status = "closed";
         $barbg = "bgcolor='#eed0d0'";
     }
     elsif ($sp->{'timelasthelp'} > $sp->{'timetouched'}+5) {
         $status = "answered<br />awaiting close";
         $barbg = "bgcolor='#eeeed0'";
     }
     elsif ($sp->{'timelasthelp'} && $sp->{'timetouched'} > $sp->{'timelasthelp'}+5) {
         $status = "answered<br /><b>still needs help</b>";
         $barbg = "bgcolor='#d0eed0'";
     }

     next if ($FORM{'state'} eq "green" && $barbg ne "bgcolor='#d0eed0'");

     # fix up the subject if needed
     eval {
         if ($sp->{'subject'} =~ /^=\?(utf-8)?/i) {
             my @subj_data;
             @subj_data = MIME::Words::decode_mimewords($sp->{'subject'});
             if (scalar(@subj_data)) {
                 if (!$1) {
                     $sp->{'subject'} = Unicode::MapUTF8::to_utf8({-string=>$subj_data[0][0], -charset=>$subj_data[0][1]});
                 } else {
                     $sp->{'subject'} = $subj_data[0][0];
                 }
             }
         }
     };

     my $summary = LJ::ehtml($sp->{'subject'});
     my $secold = time() - $sp->{'timecreate'};
     my $age = LJ::ago_text($secold);
     my $probarea = $sp->{_cat}->{'catname'};
     my $points = LJ::Support::calc_points($sp, $secold);

     unless ($status eq "closed") {
         $status .= "<br />($points point";
         if ($points > 1) { $status .= "s"; }
         $status .= ")";
     }

     $ret .= "<tr valign='top' $barbg>\n";
     $ret .= "<td><b><a href=\"see_request.bml?id=$sp->{'spid'}\">$sp->{'spid'}</a></b></td>\n";
     $ret .= "<td><b>$summary</b></td>\n";
     $ret .= "<td>$probarea</td>\n";
     $ret .= "<td nowrap='nowrap'><font size='-1'>$age</font></td>\n";
     $ret .= "<td nowrap='nowrap'><font size='-1'>$status</font></td>\n";
     $ret .= "</tr>";

 }
 $ret .= "</table>\n";

 return $ret;

_code?>

<?hr?>
<p>Back to the <a href="./">support area</a>.</p>

<=body
page?><?_c <LJDEP>
link: htdocs/support/help.bml, htdocs/support/see_request.bml
link: htdocs/support/index.bml
form: htdocs/support/help.bml
</LJDEP> _c?>
