<?_code # -*-bml-*-
{
    use strict;
    use vars qw(%FORM);
    use JSON;
    use LJ::Directory::Search;

    my $err = sub {
        my $msg = shift;
        return JSON::objToJson({
            error => "Error: $msg",
        });
    };

    BML::set_content_type('text/javascript; charset=utf-8');
    BML::finish();
    BML::noparse();

    # build list of search constraints from post/get args
    my @constraints = LJ::Directory::Constraint->constraints_from_formargs(\%FORM);
    my $dir = LJ::Directory::Search->new(page_size => 1000, constraints => \@constraints);
    my $results = eval { $dir->search } or return $err->($@);

    # get update times
    my $updated = LJ::get_timeupdate_multi(map { $_->userid } $results->users);

    my @users;
    foreach my $u ($results->users) {
        my $updatetime = $updated->{$u->userid};
        my $updatetimestring = $updatetime ? LJ::ago_text(time() - $updatetime) :
            'Never';

        push @users, {
            lastupdated => $updatetime,
            $u->info_for_js,
            lastupdated_string => $updatetimestring,
        };
    }

    my $ret = {
        users => \@users,
    };

    sleep(1) if $LJ::IS_DEV_SERVER;

    return LJ::js_dumper($ret);
}
_code?>
