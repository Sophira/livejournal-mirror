<?page
body<=
<?_code
{
    use strict;
    use vars qw(%GET $title);
    use LJ::M::ProfilePage;

    my $body = "";
    $title = "";

    # Deal with bots
    my $sep  = "**";
    my $pre  = "<!-- \n" . ($sep x 40 . "\n") x 2 . "$sep\n" x 10 . $sep;
    my $post = "\n" . "$sep\n" x 10 . ($sep x 40 . "\n") x 2 . " -->";
    $body .= LJ::run_hook("bot_director", $pre, $post);

    if ($LJ::SERVER_DOWN) {
        $title = $ML{'Sorry'};
        $body = LJ::server_down_html();
        return;
    }

    my $user = $GET{user};
    my $u = LJ::load_user($user) || LJ::get_remote();
    return BML::redirect("$LJ::SITEROOT/login.bml?ret=1") unless $u;

    my $method = $GET{'mode'} eq "of" ? "friendofs" : "friends";

    my @friends = $u->$method;
    my $count   = @friends;

    # sort on display name
    {
        my %uid_name; # uid -> display name
        foreach my $fu (@friends) {
            $uid_name{$fu->id} = $fu->display_username;
        }
        @friends = sort { $uid_name{$a->{userid}} cmp $uid_name{$b->{userid}} } @friends;
    }

    my $self_link = sub {
        my %params = map { ($_, $GET{$_}) } grep { exists $GET{$_} } qw(page nopics friendofs mode);

        $params{page} = $_[0];

        return "/tools/friendlist.bml?" . join('&', map { "$_=$params{$_}" } keys %params);
    };

    my $page_size = 100;
    my %items = BML::paging(\@friends, $GET{'page'}, $page_size);
    my $navbar = LJ::paging_bar($items{'page'}, $items{'pages'},
                                { 'self_link' => $self_link });
    @friends = @{$items{'items'}};

    LJ::Userpic->preload_default_userpics(@friends);

    $body .= $navbar;

    $body .= "Friends: $count\n";

    $body .= '<table align="center" border=0 cellspacing=3>';

    my $maxcol = 5;

  ROW:
    while (1) {
        $body .= " <tr>\n";
        for (1..$maxcol) {
            last ROW unless @friends;
            my $friend = shift @friends;
            my $pic = "";
            if (my $up = $friend->userpic) {
                $pic .= $up->imgtag_lite . "<br />\n";
            }
            $body .= "   <td valign='bottom' align='center' width=100>$pic" . $friend->ljuser_display . "</td>\n";
        }
        $body .= " </tr>\n";
    }

    $body .= "</table>\n";
    $body .= $navbar if $count > $page_size;
    return $body;
}
_code?>
<=body
title=><?_code return $title; _code?>
page?>
