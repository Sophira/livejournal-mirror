<?page
body<=
<?_code
{
    use strict;
    use vars qw(%GET %POST $title $headextra @errors @warnings);

    $title = 'SMS Settings';

    LJ::need_res(qw(
                    stc/sms.css
                    ));

    my $u = LJ::get_remote()
        or return "<?needlogin?>";

    return "Your account must be active to use this page" unless $u->is_visible;

    # get friend groups
    my $fgroupsref = LJ::get_friend_group($u);
    my @fgroups = $fgroupsref ? sort { $a->{sortorder} <=> $b->{sortorder} } values %$fgroupsref : ();

    # perhaps a hook wants us to redirect
    my $redir;

    # process changes
    if (LJ::did_post()) {

        # set carrier
        {
            if (%LJ::SMS_CARRIERS && $POST{carrier} && grep { $_ eq $POST{carrier} } keys %LJ::SMS_CARRIERS) {
                $u->set_prop('sms_carrier', $POST{carrier});

            # invalid carrier
            } elsif ($POST{carrier}) {
                push @errors, 'Invalid carrier selection';

            # clear carrier
            } elsif (! $POST{carrier} && ! $POST{msisdn}) {
                $u->set_prop('sms_carrier', '');
            }
        }

        my $new_msisdn = $POST{msisdn};

        # user posted but with blank msisdn, clear it if it's mapped
        $u->delete_sms_number unless $new_msisdn;

        # strip out invalid characters
        $new_msisdn =~ s/[^\+\d]//g if $new_msisdn;

        # normalize given number to actual msisdn for storage in db
        if ($new_msisdn =~ /^\+?1?\d{10}$/ && $u->prop('sms_carrier')) {
            # strip off leading + and leading 1, then re-add to normalize
            $new_msisdn =~ s/^\+//;
            $new_msisdn =~ s/^1//;
            $new_msisdn = "+1$new_msisdn";

            # if user has clicked reregister or entered a number which doesn't
            # match their current mapping...
            if ($POST{reregister} || $u->sms_mapped_number ne $new_msisdn) {
                # in this instance we're looking to see if someone already has this
                # number verified in order to check for number stealing below
                my $existing_num_uid = LJ::SMS->num_to_uid($new_msisdn, verified_only => 1);

                # don't let them steal a number!
                if ($existing_num_uid && $existing_num_uid != $u->id) {
                    push @errors, "That number is currently in use by another user.";

                # check rate limiting
                } elsif (! $u->rate_log('sms_register', 1)) {
                    push @errors, "You have tried to register a number too many times in one day. " .
                        "Please wait and try again.";

                } else {
                    # map the number
                    $u->set_sms_number($new_msisdn, verified => 'N'); # not verified

                    # run the post-reg hook
                    LJ::run_hook('sms_post_register', u => $u, redir => \$redir, errors => \@errors);
                }
            }
        } elsif (! $u->prop('sms_carrier') && $POST{msisdn}) {
            # tried to set number but no carrier set
            push @errors, "You must select a carrier in order to register";
        } else {
            # user has not entered a valid number for registration
            push @errors, "You must enter a valid U.S. 10-digit phone number" if $POST{msisdn};
        }

        if ($POST{fgroup} && grep { $_->{groupname} eq $POST{fgroup} } @fgroups) {
            $u->set_prop('sms_friend_group', $POST{fgroup});
        } elsif ($POST{fgroup}) {
            push @errors, "Invalid friend group";
        } else {
            # no friend group, clear the prop
            $u->set_prop('sms_friend_group', '');
        }

        LJ::run_hook('sms_bml_post', u => $u, POST => \%POST,
                     errors => \@errors, warnings => \@warnings,
                     redir => \$redir);

        return BML::redirect($redir) if $redir;
    }

    # FIXME: why is this called so far from its 1 use?
    my $status = LJ::run_hook("sms_status_msg", $u);

    my $msisdn = LJ::html_text
        ({ name => 'msisdn',
           value => ($POST{msisdn} || $u->sms_mapped_number) });

    # build list of carriers for display
    my $carrier;
    if (%LJ::SMS_CARRIERS) {
        my @carriers = ();

        while ( my ($k, $v) = each %LJ::SMS_CARRIERS) {
            $v .= '<super>&reg;</super>';
            push @carriers, ($k, $v);
        }

        $carrier = LJ::html_select({noescape => 1, name => 'carrier', selected => $u->prop('sms_carrier')},
                                   ('', '(Select Carrier)'), @carriers);
    }

    # FIXME: localize this to its one caller
    my $defsec = "Journal Default (Public)";

    my $fgroup = LJ::html_select({name => 'fgroup', selected => $u->prop('sms_friend_group')},
                                ('', '(Default)'), map { $_, $_ } map { $_->{groupname} } @fgroups);

    my $ad = LJ::ads(type => 'app', orient => 'App-SMSSettings', force => 1);

    my $body = '';

    my $carrier_html = $carrier || '';

    $body .= qq {
        <div>Setting up $LJ::SITENAMESHORT with your mobile phone allows you to easily post to your journal,
        read your friends page, get notified of recent comments, and <a href="what">more...</a></div>

        <h2>Setup</h2>
        <div>Enter in your full phone number and select your service provider, a confirmation message will be
        sent to your phone.</div>

        <form method="POST" action="$LJ::SITEROOT/manage/settings/sms/index.bml">

        <table id="SMS_Setup">
        <tr><td class="Label">Status:</td><td>$status</td></tr>
        <tr><td class="Label">Full Phone Number:</td><td>$msisdn</td></tr>
        <tr><td class="Label">Carrier:</td><td>$carrier</td></tr>
        <tr><td class="Label">Default SMS Post Security:</td><td>$defsec</td></tr>
        <tr><td class="Label">Default SMS Friends Page Display:</td><td>$fgroup</td></tr>
        </table>
        <div id="SMS_Ad">$ad</div>
    };

    $body .= LJ::run_hook('sms_bml_content', $u);

    $body .= qq {
        <input type="submit" value="Submit" id="sms_submit" />
        </form>
    };

    return $body;
}
_code?>
<=body
title=><?_code return $title; _code?>
head<=
<?_code return $headextra; _code?>
<=head
<=body
page?>
