(=PAGE
TITLE=>Read Comments
BODY<=

(=_CODE
#BML:cache:talkread

 use strict;
 use vars qw($r_head %FORM);

 $r_head = "";
 my $head = $_[1] ? \$_[1]->{'head'} : \$r_head;

 my $PAGE_SIZE = 25;
 my $THREADING_POINT = 50;

 return LJ::server_down_html() if ($LJ::SERVER_DOWN);

 my $dbs = LJ::get_dbs();
 my $dbh = $dbs->{'dbh'};
 my $dbr = $dbs->{'reader'};
 my ($sth, $sql);
 my $errtxt;

 my $pics = LJ::Talk::get_subjecticons();

 # pre-load common strings for little speed and less typing later
 my %T = qw(postcomments   talk.commentpost
            readcomments   talk.commentsread
            parent         talk.parentlink
            thread         talk.threadlink
            replythis      talk.replytothis
            link           talk.commentpermlink
            );
 foreach (keys %T) { $T{$_} = BML::ml($T{$_}); }

 ## workaround mail client bug when don't understand quoted-printable.
 ## Only correct 'journal' if 'itemid' was also broken, to avoid the 
 ## unlikely clash with a journal name.
 if ($FORM{'itemid'} =~ s/^3D//) {
     $FORM{'journal'} =~ s/^3D//;
     $FORM{'thread'} =~ s/^3D//; 
 }
 my $init = LJ::Talk::init($dbs, \%FORM);
 my $u = $init->{'journalu'};

 return "(=H1 Error H1=)(=P $init->{'error'} P=)" if $init->{'error'};
 return "Error: couldn't determine journal from arguments." unless $u;

 my $clustered = $init->{'clustered'};

 my ($dbcs, $dbcm, $dbcr) = ($dbs, $dbh, $dbr);
 if ($clustered) {
     $dbcs = LJ::get_cluster_set($u);
     $dbcm = $dbcs->{'dbh'};
     $dbcr = $dbcs->{'reader'};
 }

 my $jarg = $u->{'clusterid'} ? "journal=$u->{'user'}&" : "";
 my $jargent = $u->{'clusterid'} ? "journal=$u->{'user'}&amp;" : "";

 my $SELF = "/talkread.bml";
 my $ret = "";

 my $itemid = $init->{'itemid'}+0;

 my $item = LJ::Talk::get_journal_item($dbs, $dbcs, $u, $itemid);

 if ($init->{'oldurl'} && $item) {
     $init->{'anum'} = $item->{'anum'};
     $init->{'ditemid'} = $init->{'itemid'}*256 + $item->{'anum'};
 }

 unless ($item && $item->{'anum'} == $init->{'anum'}) {
     return "(=H1 Error H1=)(=P No such entry. P=)";
 }

 my %logprops = %{$item->{'logprops'}};

 my $ditemid = $init->{'ditemid'}+0;
 my $page = int($FORM{'page'}) || 1;
 my $thread = $init->{'thread'};

 ### load users
 my ($up);  # $up = user posted journal item
 LJ::load_userids_multiple($dbs, [
                                  $item->{'posterid'} => \$up,
                                  ], [ $u ]);
 LJ::load_user_props($dbs, $up, "opt_showtopicstuff");

 LJ::text_out(\$u->{'name'});

 # check suspended
 return "(=H1 Suspended H1=)(=P This journal/poster is suspended. P=)"
 if ($u->{'statusvis'} eq "S" || $up->{'statusvis'} eq "S");

 my $remote = LJ::get_remote($dbs);

 ####  Check security before viewing this post
 return $errtxt unless LJ::Talk::check_viewable($dbs, $remote, $item, \%FORM, \$errtxt);

 my $event = $item->{'event'};

 LJ::expand_embedded($dbs, $ditemid, $remote, \$event);
 $event = BML::ebml(LJ::CleanHTML::clean_event(\$event, $logprops{$itemid}->{'opt_preformatted'}));

 $ret .= "<p>";
 $ret .= "<table><tr valign='middle'>";

 my $picid;
 if ($logprops{$itemid}->{'picture_keyword'}) {
     my $qkw = $dbr->quote($logprops{$itemid}->{'picture_keyword'});
     $picid = $dbr->selectrow_array("SELECT m.picid FROM userpicmap m, keywords k ".
                                    "WHERE m.userid=$up->{'userid'} ".
                                    "AND m.kwid=k.kwid AND k.keyword=$qkw");
 }

 $picid ||= $up->{'defaultpicid'};

 my %userpics;
 if ($picid) {
     LJ::load_userpics($dbs, \%userpics, [ $picid ]);
     my $alt = $up->{'name'};
     if ($logprops{$itemid}->{'picture_keyword'}) {
         $alt .= ": $logprops{$itemid}->{'picture_keyword'}";
     }
     LJ::text_out(\$alt);
     $alt = LJ::ehtml($alt);
     $ret .= "<td><img src='/userpic/$picid' width='$userpics{$picid}->{'width'}' ".
         "height='$userpics{$picid}->{'height'}' align='absmiddle' ".
         "hspace='3' title='$alt' alt=''></td>";
 }

 $ret .= "<td>";
 if ($up->{'user'} eq $u->{'user'}) {
     $ret .= BML::ml("talk.somebodywrote", { 'realname' => BML::eall($up->{'name'}),
                                             'userlink' => LJ::ljuser($up->{'user'}) });
 } else {
     $ret .= BML::ml("talk.somebodywrote_comm", { 'realname' => BML::eall($up->{'name'}),
                                                  'userlink' => LJ::ljuser($up->{'user'}),
                                                  'commlink' => "(=LJCOMM $u->{'user'} LJCOMM=)" });
 }

 my $etime = $item->{'eventtime'};
 $etime =~ s!(\d\d\d\d)-(\d\d)-(\d\d)!LJ::date_to_view_links($u, $&)!e;
 $ret .= "<br /><font size='-1'>@ $etime</font>";
 $ret .= "</td></tr></table>";

 ## standout bar
 $ret .= LJ::Talk::link_bar($dbs, { 'u' => $u, 'up' => $up, 'headref' => $head,
                                    'remote' => $remote, 'itemid' => $ditemid, });

 ## dump the log entry, unless we're browsing a thread.
 unless ($thread)
 {
     my %current;
     if ($logprops{$itemid}->{'current_mood'} || $logprops{$itemid}->{'current_moodid'}) {
         LJ::load_moods($dbs);
         $current{'Mood'} = $logprops{$itemid}->{'current_mood'};
         my $mid = $logprops{$itemid}->{'current_moodid'};
         if ($mid) {
             my $theme = $up->{'moodthemeid'};
               LJ::load_mood_theme($dbs, $theme);
             my %pic;
             if (LJ::get_mood_picture($theme, $mid, \%pic)) {
                 $current{'Mood'} = "<img src='$pic{'pic'}' align='absmiddle' width='$pic{'w'}' height='$pic{'h'}' vspace='1'> $LJ::CACHE_MOODS{$mid}->{'name'}";
             } else {
                 $current{'Mood'} = $LJ::CACHE_MOODS{$mid}->{'name'};
             }
         }
     }
     if ($logprops{$itemid}->{'current_music'}) {
         $current{'Music'} = $logprops{$itemid}->{'current_music'};
     }

     $ret .= "<ul>"; # using <ul> for intenting isn't proper, but this will all go away in s2 anyway.

     ### currents
     if (%current) {
         $ret .= "<table border=0>\n";
         foreach (sort keys %current) {
             $ret .= "<tr><td align=right><b>Current $_:</b></td><td>$current{$_}</td></tr>\n";
         }
         $ret .= "</table><p>\n";
     }

     ### security indicator
     my $sec = "";
     if ($item->{'security'} eq "private") {
         $sec = "(=SECURITYPRIVATE=)";
     } elsif ($item->{'security'} eq "usemask") {
         $sec = "(=SECURITYPROTECTED=)";
     }

     $sec .= "<br />\n" unless $sec eq "" or $item->{'subject'};
     $ret .= $sec;

     ###
     if ($item->{'subject'}) {
         my $subject = $item->{'subject'};
       LJ::CleanHTML::clean_subject(\$subject);
         $ret .= "<font face='Arial,Helvetica' size='+1'><i><b>$subject</b></i></font><br />\n";
     }

     $ret .= $event;
     $ret .= "</ul>";
 }


 $ret .= "<br clear='all' /><hr width='100%' size='2' align='center' />";
 ### is this in a topic?
 $ret .= LJ::Talk::topic_links($dbs, $u, $ditemid) unless $FORM{'replyto'};

 my %users_to_load;
 my @posts_to_load;
 my %posts;
 my %children;

 my $need_subjects = 0;  # if set, we load subjects later. (cluster0 only)

 # get all talk rows & their subjects.  will load needed body text later.
 {
     # clustered always gets from a reader (no recent tables exist).
     # otherwise, need to decide if it's safe to get all data from a
     # slave.  if the owning post is newer than $LJ::RECENT_DAYS, then
     # obviously all replies are newer to, and they all exist on a
     # slave.  or, if slaves are full copies, then it's also okay.
     my $db = $dbcr;
     my $recent = "";
     if (! $clustered && $LJ::USE_RECENT_TABLES) {
         if ($item->{'secondsold'} < $LJ::RECENT_DAYS*86400) {
             $recent = "recent_";
         } else {
             $db = $dbh;
         }
     }

     unless ($db) {
         return "This part of the database is temporarily down for maintenance.  Try again in a few minutes.";
     }

     if ($clustered) {
         $sth = $db->prepare("SELECT t.jtalkid AS 'talkid', t.posterid, u.user AS 'userpost', ".
                             "t.datepost, t.parenttalkid, tt.subject, t.state ".
                             "FROM talk2 t, talktext2 tt ".
                             "LEFT JOIN useridmap u ON u.userid=t.posterid ".
                             "WHERE t.journalid=$u->{'userid'} AND tt.journalid=$u->{'userid'} ".
                             "AND t.nodetype='L' AND t.nodeid=$itemid AND ".
                             "t.jtalkid=tt.jtalkid ORDER BY t.jtalkid");
     } else {
         $need_subjects = 1;
         $sth = $db->prepare("SELECT t.talkid, t.posterid, u.user AS 'userpost', ".
                             "t.datepost, t.parenttalkid, t.state ".
                             "FROM talk t LEFT JOIN useridmap u ON u.userid=t.posterid ".
                             "WHERE t.nodetype='L' AND t.nodeid=$itemid ".
                             "ORDER BY t.talkid");
     }
     $sth->execute;
     die $db->errstr if $db->err;
 }
 my $post_count = 0;
 while (my $post = $sth->fetchrow_hashref) {
     $post_count++ unless ($post->{'state'} eq "D");
     $posts{$post->{'talkid'}} = $post;
     push @{$children{$post->{'parenttalkid'}}}, $post->{'talkid'};
 }

 # we let the page size initially get bigger than normal for awhile,
 # but if it passes threading_point, then everything's in page_size
 # chunks:
 $PAGE_SIZE = $THREADING_POINT if $post_count < $THREADING_POINT;

 if ($need_subjects && $post_count > $PAGE_SIZE) {
     # if we didn't load subjects (for thread links), and we'll need to thread,
     # then we need to load 'em, otherwise they'll be loaded later by the
     # full get_talktext load.
     # FIXME: ideally, we'd only load the stuff that threads, and not
     #        everything.  we should do that for clusters later.
     my $tt = LJ::get_talktext($dbs, {'onlysubjects'=>1}, keys %posts);
     foreach (keys %posts) {
         $posts{$_}->{'subject'} = $tt->{$_}->[0];
     }
 }


 $children{$thread} ||= [];  # strictness doesn't allow treating undef as arrayref

 my $top_replies = $thread ? 1 : scalar(@{$children{$thread}});
 my $pages = int($top_replies / $PAGE_SIZE);
 if ($top_replies % $PAGE_SIZE) { $pages++; }

 my @top_replies = $thread ? ($thread) : sort { $posts{$a}->{'datepost'} cmp $posts{$b}->{'datepost'} } @{$children{$thread}};
 foreach (@top_replies) {  $posts{$_}->{'_hide'} = 1; }
 my $itemfirst = $PAGE_SIZE * ($page-1) + 1;
 my $itemlast = $page==$pages ? $top_replies : ($PAGE_SIZE * $page);

 @top_replies = @top_replies[$itemfirst-1 .. $itemlast-1];

 foreach (@top_replies) {  delete $posts{$_}->{'_hide'} }

 push @posts_to_load, @top_replies;
 if (@posts_to_load < $PAGE_SIZE) {
     # then let's show some more!
     my @check_for_children = @posts_to_load;
     while (@check_for_children && @posts_to_load < $PAGE_SIZE)
     {
         my $cfc = shift @check_for_children;
         next unless (defined $children{$cfc});
         foreach my $child (sort { $posts{$a}->{'datepost'} cmp $posts{$b}->{'datepost'} } @{$children{$cfc}}) {
             last unless (@posts_to_load < $PAGE_SIZE);
             push @check_for_children, $child;
             push @posts_to_load, $child;
         }
     }
 }

 unless (@posts_to_load) {
     $ret .= "No replies";
 }

 ### loads more of some of the posts, uses slaves if possible.
 my $posts_loaded;
 if ($clustered) {
     $posts_loaded = LJ::get_talktext2($u, @posts_to_load);
 } else {
     $posts_loaded = LJ::get_talktext($dbs, @posts_to_load);
 }
 foreach my $talkid (keys %{$posts_loaded}) {
     $posts{$talkid}->{'subject'} = $posts_loaded->{$talkid}->[0];
     $posts{$talkid}->{'body'} = $posts_loaded->{$talkid}->[1];
     $posts{$talkid}->{'_loaded'} = 1;
     $users_to_load{$posts{$talkid}->{'posterid'}} = 1;
 }

 ### load meta-data
 my $talkid_in = join(",", @posts_to_load);
 if ($clustered) {
     LJ::load_props($dbs, "talk");
     LJ::load_talk_props2($dbcr, $u->{'userid'}, \@posts_to_load, \%posts);
 } else {
     LJ::load_talk_props($dbs, \@posts_to_load, \%posts);
 }

 if ($LJ::UNICODE) {
     foreach (@posts_to_load) {
         if ($posts{$_}->{'unknown8bit'}) {
             LJ::item_toutf8($dbs, $u, \$posts{$_}->{'subject'},
                             \$posts{$_}->{'body'},
                             {});
         }
     }
 }

 my %user;
 foreach my $talkid (sort { $a <=> $b } keys %posts)
 {
     $_ = $posts{$talkid};
     next if ($_->{'_hide'});

     if ($_->{'state'} eq "D") {
         $_->{'subject'} = "[deleted]";
     }
     elsif ($_->{'state'} eq "A") {
         # if the message isn't deleted, flag its parent(s) as
         # having children to recurse into later.
         my $par = $_->{'parenttalkid'};
         while ($par) {
             $posts{$par}->{'_haschildren'}++;
             $par = $posts{$par}->{'parenttalkid'};
         }

         unless ($user{$_->{'userpost'}}) {
             $user{$_->{'userpost'}} = {};
             if ($_->{'_loaded'}) {
                 $users_to_load{$_->{'posterid'}} = 1;
             }
         }
     }
 }

 my %userpics = ();
 if (%users_to_load) {
     my @pics_to_load;
     my $userid_in = join(", ", keys %users_to_load);
     $sth = $dbr->prepare("SELECT user, statusvis, email, name, defaultpicid FROM user ".
                          "WHERE userid IN ($userid_in)");
     $sth->execute;

     while ($_ = $sth->fetchrow_hashref) {
         $user{$_->{'user'}} = $_;
         push @pics_to_load, $_->{'defaultpicid'} if $_->{'defaultpicid'};
     }
     LJ::load_userpics($dbs, \%userpics, [ @pics_to_load ]);

 }

 ########## make the navcrap
 my $navcrap;
 if ($pages > 1) {
     $navcrap .= "(=STANDOUT <center><font face='Arial,Helvetica' size='-1'><b>";
     $navcrap .= "Page $page of $pages<br />";
     my $left = "<b>&lt;&lt;</b>";
     if ($page > 1) { $left = "<a href='" . LJ::self_link(\%FORM, { 'page' => $page-1 }) . "'>$left</a>"; }
     my $right = "<b>&gt;&gt;</b>";
     if ($page < $pages) { $right = "<a href='" . LJ::self_link(\%FORM, { 'page' => $page+1 }) . "'>$right</a>"; }
     $navcrap .= $left . " ";
     for (my $i=1; $i<=$pages; $i++) {
         my $link = "[$i]";
         if ($i != $page) { $link = "<a href='" . LJ::self_link(\%FORM, { 'page' => $i }) . "'>$link</a>"; }
         else { $link = "<font size='+1'><b>$link</b></font>"; }
         $navcrap .= "$link ";
     }
     $navcrap .= "$right";
     $navcrap .= "</font></center> STANDOUT=)\n";
 }
 ####### end navcrap

 unless ($logprops{$itemid}->{'opt_nocomments'} or $u->{'opt_showtalklinks'} eq "N")
 {
     $ret .= "<p>$navcrap</p>" if $navcrap;
     my $readlink;
     if ($FORM{'thread'} && $pages == 1) {
         $readlink = "(<a href='talkread.bml?${jargent}itemid=$ditemid'>$T{'readcomments'}</a>) - ";
     }
     $ret .= "<center><b>$readlink(<a href='talkpost.bml?${jargent}itemid=$ditemid'>$T{'postcomments'}</a>)</b></center>";

     if ($post_count > 0)
     {
         recurse_post(\%posts, \$ret, $thread, {}, { "newfirst" => 0,
                                                     "children" => \%children,
                                                 });
         $ret .= "<p><hr /><center><b>$readlink(<a href='talkpost.bml?${jargent}itemid=$ditemid'>$T{'postcomments'}</a>)</b></center></p>";
     }

     if ($navcrap) {
         $ret .= "<p>$navcrap</p>";
     }
 }


sub recurse_post
{
    my ($postref, $ret, $tid, $mark, $opts) = @_;
    # don't let people make circular references
    return if ($mark->{$tid});
    $mark->{$tid} = 1;

    my $bgcolor = ($opts->{'depth'} % 2) ? "(=EMCOLORLITE=)" : "(=EMCOLOR=)";

    # find child messages (replys to $tid)
    my @childs = sort { $postref->{$a}->{'datepost'} cmp $postref->{$b}->{'datepost'} }
    grep { ($postref->{$_}->{'state'} ne "D" || $postref->{$_}->{'_haschildren'}) }
    @{$opts->{'children'}->{$tid}};

    #$$ret .= "<p>recurse_post($tid): children=(@childs)\n";

    return unless ($tid || @childs);

    my $post = $postref->{$tid};

    # display new or old first?
    if ($opts->{'newfirst'}) { @childs = reverse @childs; }

    if ($tid)
    {
        my $dtid = $clustered ? ($tid * 256 + $init->{'anum'}) : $tid;

        my $datepost = "<font size='-1'>" . substr($post->{'datepost'}, 0, 16) . "</font>";
        my $userpost = $post->{'userpost'};
        my $user = "<i>(Anonymous)</i>";
        if ($post->{'deleted_poster'}) {
            $user = "<i>(Deleted user: $post->{'deleted_poster'})</i>";
        }

        if ($post->{'state'} eq "D") {
            $$ret .= "<p><a name='t$dtid'></a><table><tr>";
            $$ret .= "<td><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
            $$ret .= "<td><b>(Deleted post)</b></td></tr></table>\n";
        } elsif ($userpost && $user{$userpost}->{'statusvis'} eq "S") {
            $$ret .= "<p><a name='t$dtid'></a><table><tr>";
            $$ret .= "<td><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
            $$ret .= "<td><b>(Reply from suspended user)</b></td></tr></table>\n";
        } else {
            if ($userpost) {
                $user = "(=LJUSER $userpost LJUSER=)";
            }
            my $icon = LJ::Talk::show_image($pics, $post->{'subjecticon'});

            if ($post->{'_loaded'}) {
                $$ret .= "<p><a name='t$dtid'></a><table width='100%'><tr>";
                $$ret .= "<td rowspan='2'><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
                $$ret .= "<td bgcolor='$bgcolor' width='100%'>";
                if ($userpost) {
                    my $picid = $user{$userpost}->{'defaultpicid'};
                    my $pickw = $post->{'picture_keyword'};
                    if ($pickw) {
                        $picid = $user{$userpost}->{'_pictures'}->{$pickw};
                        unless ($picid) {
                            my $qkw = $dbr->quote($pickw);
                            my $sth = $dbr->prepare("SELECT m.picid FROM userpicmap m, keywords k ".
                                                    "WHERE m.userid=$post->{'posterid'} ".
                                                    "AND m.kwid=k.kwid AND k.keyword=$qkw");
                            $sth->execute;
                            my ($alt_picid) = $sth->fetchrow_array;
                            if ($alt_picid) {
                                LJ::load_userpics($dbs, \%userpics, [ $alt_picid ]);
                                $picid = $alt_picid;
                                $user{$userpost}->{'_pictures'}->{$pickw} = $picid;
                            }
                        }
                    }
                    $picid ||= $user{$userpost}->{'defaultpicid'};
                    if ($picid) {
                        my $alt = $user{$userpost}->{'name'};
                        if ($post->{'picture_keyword'}) {
                            $alt .= ": $post->{'picture_keyword'}";
                        }
                        $alt = LJ::ehtml($alt);

                        $$ret .= "<img align='left' hspace='3' src='/userpic/$picid'";
                        $$ret .= " width='$userpics{$picid}->{'width'}'";
                        $$ret .= " title='$alt' alt=''";
                        $$ret .= " height='$userpics{$picid}->{'height'}' />";
                    }
                }

                my $cleansubject = LJ::ehtml($post->{'subject'});

                $$ret .= "<font size='+1' face='Arial,Helvetica'><b>$cleansubject</b></font> $icon";
                $$ret .= "<br />$user\n";
                $$ret .= "<br />$datepost\n";
                if ($post->{'poster_ip'} && $remote &&
                    ($remote->{'user'} eq $up->{'user'} ||
                     LJ::check_priv($dbs, $remote, "sharedjournal", $u->{'user'})))
                {
                    $$ret .= "(from $post->{'poster_ip'})";
                }

                $$ret .= " <font size='-1'>(<a href='$SELF?${jargent}itemid=$ditemid&amp;thread=$dtid#t$dtid'>$T{'link'}</a>)</font> ";

                if ($remote && ($remote->{'user'} eq $userpost ||
                                $remote->{'user'} eq $u->{'user'} ||
                                $remote->{'user'} eq $up->{'user'} ||
                                LJ::check_priv($dbs, $remote, "sharedjournal", $u->{'user'})))
                {
                    $$ret .= "<a href='delcomment.bml?${jargent}id=$dtid'>" . LJ::img("btn_del", "", { 'align' => 'absmiddle', 'hspace' => 2, 'vspace' => }) . "</a>";
                }

                $$ret .= "</td></tr>";
                $$ret .= "<tr><td>\n";

                ## escape BML block codes.  so (=TAG ... TAG=) turns into (&#0061; ... TAG=)
                $post->{'body'} =~ s/\(=/\(&\#0061\;/g;
                LJ::CleanHTML::clean_comment(\$post->{'body'}, $post->{'opt_preformatted'});
                $$ret .= $post->{'body'};

                $$ret .= "<p><font size='-2'>";
                if ($post->{'parenttalkid'} != 0) {
                    my $dpid =  $clustered ? ($post->{'parenttalkid'} * 256 + $init->{'anum'}) : $post->{'parenttalkid'};
                    $$ret .= "(<a href='$SELF?${jargent}itemid=$ditemid&amp;thread=$dpid'>$T{'parent'}</a>) ";
                }
                if (defined $opts->{'children'}->{$tid}) {
                    $$ret .= "(<a href='$SELF?${jargent}itemid=$ditemid&amp;thread=$dtid'>$T{'thread'}</a>) ";
                }

                $$ret .= "(<a href='/talkpost.bml?${jargent}replyto=$dtid'>$T{'replythis'}</a>)</font>";
                $$ret .= "</td></tr></table>\n";  # close colored table
            } else {
                # link to message

                # convert the subject to UTF-8 if needed
                if ($LJ::UNICODE && ! LJ::is_utf8($post->{'subject'})) {
                    my $error;
                    my $subj = LJ::text_convert($dbs, $post->{'subject'}, 
                                                $u, \$error);
                    if ($error) {
                        LJ::text_out(\$post->{'subject'});
                    } else {
                        $post->{'subject'} = $subj;
                    }
                }

                $$ret .= "<a name='t$dtid'></a><table><tr>";
                $$ret .= "<td><img src='$LJ::IMGPREFIX/dot.gif' height='1' width='" . ($opts->{'depth'} * 25) . "'></td>";
                $$ret .= "<td><a href='$SELF?${jargent}itemid=$ditemid&amp;thread=$dtid'>" . BML::eall($post->{'subject'} || "(no subject)") . "</a> - $user, <i>$post->{'datepost'}</i></td></tr></table>\n";
            }
        }
    }

    if (@childs)
    {
        my $count;
        # go through children and show them too.
        foreach my $id (@childs) {
            next if ($postref->{$id}->{'_hide'});
            recurse_post($postref, $ret, $id, $mark, { "depth" => $opts->{'depth'} + ($tid ? 1 : 0),
                                                       "children" => $opts->{'children'},
                                                   });
        }
    } else {
#       $$ret .= "No sub-posts.";
    }
}


LJ::strip_bad_code(\$ret);
return $ret;

_CODE=)
<=BODY
HEAD=>(=_CODE return $_[1] ? $_[1]->{'head'} : $r_head _CODE=)
PAGE=)(=_C <LJDEP>
link: htdocs/userinfo.bml, htdocs/go.bml, htdocs/tools/memadd.bml, htdocs/editjournal_do.bml, htdocs/topics/additem.bml
link: htdocs/tools/tellafriend.bml, htdocs/talkpost.bml, htdocs/talkread.bml, htdocs/delcomment.bml
img: htdocs/img/btn_prev.gif, htdocs/img/memadd.gif, htdocs/img/btn_edit.gif, htdocs/img/topic_add.gif, htdocs/img/btn_tellafriend.gif
img: htdocs/img/btn_next.gif, htdocs/img/dot.gif, htdocs/img/delcomment.gif
</LJDEP> _C=)

