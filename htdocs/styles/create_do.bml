<?page
title=>Create Style
body<=

<?_code
{
    use strict;
    use vars qw(%GET %POST);

    return LJ::server_down_html() if $LJ::SERVER_DOWN;
    return BML::redirect("create.bml") unless LJ::did_post();

    my $remote = LJ::get_remote();
    return LJ::bad_input("You must be logged in to use this tool.")
        unless $remote;

    my $authas = $GET{'authas'} || $remote->{'user'};
    my $u = LJ::get_authas_user($authas);
    return LJ::bad_input("You could not be authenticated as the specified user.")
        unless $u;
 
    unless (LJ::get_cap($u, "styles")) {
        return LJ::bad_input("Your account type doesn't permit you to make styles.");
    }

    # error check submission
    return LJ::bad_input("You did not select a valid style type.")
        unless $POST{'type'} && defined $LJ::viewinfo{$POST{'type'}};

    my $view = $POST{'type'};
    my $base = "";
    my $baseid = $POST{"base_$view"} || $POST{"basenum_$view"};

    # get a database handle
    my $dbh = LJ::get_db_writer();

    if ($baseid) {
        my $rec = $dbh->selectrow_hashref("SELECT user, is_public, formatdata FROM style WHERE styleid=? AND type=?",
                                          undef, $baseid, $view);

        return LJ::bad_input("The style you're trying to make a new style from ($baseid), does not seem to exist.")
            unless $rec;

        return LJ::bad_input("The style you're trying to make a new style from ($baseid) is not public so you cannot copy it.")
            unless $rec->{'is_public'} eq "Y" || $rec->{'user'} eq $u->{'user'};

        # they're allowed to base on this style
        $base = $rec->{'formatdata'};
    }

    ### start of output

    my $ret = "";
    my $des = "$u->{'user'}-$view-new";

    # let's see if they accidentally double-clicked:
    my $double_clicked = 0;
    my $new_id = 0;
    my ($oldid, $olddata) = $dbh->selectrow_array("SELECT styleid, formatdata FROM style WHERE user=? AND styledes=? AND type=?",
                                                  undef, $u->{'user'}, $des, $view);

    # closure to show edit form
    my $editform = sub {
        my $getextra = "?authas=$authas" if $u->{'user'} ne $remote->{'user'};
        return "<div align='center'><form method='post' action='$LJ::SITEROOT/styles/edit_do.bml$getextra'>\n" .
               LJ::html_hidden('mode', 'editstyle', 'styleid', $_[0]) . "\n" .
               LJ::html_submit(undef, "Edit Style") . "</form></div>\n";
    };

    if ($oldid) {
        if ($olddata eq $base) {
            # the user double-clicked
            $double_clicked = 1;
            $new_id = $oldid;

        } else {
            $ret .= "<?h1 Error h1?><?p You already have a style of this type named <b>$des</b>.  If you really want to create a new style of type <b>$view</b>, then go modify your old $view style, change its name, and then come back here to create a new style of this type. p?>";

            $ret .= $editform->($oldid);
            return $ret;
        }
    }

    unless ($double_clicked) {
        $dbh->do("INSERT INTO style (user, styledes, type, formatdata) VALUES (?, ?, ?, ?)",
                 undef, $u->{'user'}, $des, $view, $base);
        $new_id = $dbh->{'mysql_insertid'};
    }

    $ret .= "<?h1 Success h1?><?p Your style has been created and has been temporarily named <b>$des</b>.  You can begin editing it by pressing the button below.... p?>\n";

    $ret .= $editform->($new_id);

    return $ret;

}
_code?>

<=body
page?><?_c <LJDEP>
post: htdocs/styles/edit_do.bml
</LJDEP> _c?>
