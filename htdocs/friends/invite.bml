<?_code
{
    use strict;
    use vars qw(%GET $title $body);

#TODO: English strip all text from file :-) (file isn't ljcom but mentions lj by name)
#TODO: Strip images and offsite links from email body

    $title = "Invite a Friend!";
    $body = "";

    my $err = sub {
        $title = $ML{'.error'};
        $body = LJ::bad_input(@_);
        return;
    };

    my $u = LJ::get_remote();
    return $err->($ML{'error.noremote'}) unless $u;

    my %ierr;
    my ($capid, $anum);

    my $validate_form = sub {
        my $rv = 1;
        my $bogus = sub {
            my $key = shift;
            my $msg = shift;
            $ierr{$key} = $msg;
            $rv = 0;
        };

        if ($POST{'email'}) {
            my @errs;
            LJ::check_email($POST{'email'}, \@errs);
            $bogus->("email", "Bogus looking email: @errs") if @errs;
        } else {
            $bogus->("email", "Please enter your friend's email address");
        }

        $bogus->("name", "Please enter your friend's name") unless $POST{'name'};

        ($capid, $anum) = LJ::Captcha::session_check_code($POST{captcha_chal}, $POST{answer});
        $bogus->('captcha', 'Invalid answer to previous challenge.  Try another.') unless $capid && $anum;

        return $rv;
    };

    # inline error
    my $inerr = sub {
        my $key = shift;
        my $post = shift || "";
        return "" unless $ierr{$key};
        return "<font color='red'><b>$ierr{$key}</b></font>$post";
    };

    if (LJ::did_post() && $validate_form->()) {

        if (LJ::rate_log($u, 'invitefriend', 1)) {

            LJ::send_mail({
                to       => $POST{'email'},
                toname   => $POST{'name'},
                from     => $LJ::BOGUS_EMAIL,
                fromname => $u->{name},
                subject  => "Invitation to join $LJ::SITENAMESHORT",
                body     => $POST{'msg'},
            });

            $body .= "<?standout Message sent to $POST{email}!  Want to invite somebody else? standout?>";
            LJ::Captcha::expire($capid, $anum, $u->{userid});

            # Empty out the form and create a new captcha
            delete $POST{answer};
            delete $POST{name};
            delete $POST{email};
            delete $POST{captcha_chal};
            undef $capid;
            undef $anum;
       # Over rate limit
       } else {
           $body = "It looks like you are inviting a lot of your friends to $LJ::SITENAMESHORT.  To help us combat SPAM, you may wish to send the rest of your invitations via your own email address.  Make sure you include the invite link in your email which you can find <a href='/friends/invite.bml'>at the top of the page</a>.";
           return;
       }
    }

    my $create_link = "$LJ::SITEROOT/create.bml?from=$u->{user}";

    my $msg = $POST{'msg'};
    $msg ||= "Hi,

I'm inviting you to join $LJ::SITENAMESHORT, where you can create a personal journal (or blog) for free.

Follow this link to sign up:
$create_link

This link will take you to a page where you can create a LiveJournal account.  My journal will automatically be added as your friend, and you'll also see a list of some of my communities and choose which ones you'd like to join.

-- $u->{name} ($LJ::SITENAMEABBREV user: $u->{user})";


    # Generate captcha html
    my $captcha_body;
    my $wants_audio = $POST{answer} eq 'AUDIO' ? 1 : 0;
    {
        my ($captcha_chal, $captcha_sess);
        $captcha_chal = $POST{captcha_chal} || LJ::challenge_generate(900);
        $captcha_sess = LJ::get_challenge_attributes($captcha_chal);

        my $answer = $POST{answer};
        undef $answer if $ierr{'captcha'} || $wants_audio;

        my $try = 0;
        if ($POST{captcha_chal}) {
            $try = $u->selectrow_array('SELECT trynum FROM captcha_session ' .
                                       'WHERE sess=?', undef, $captcha_sess);
        }

        $captcha_body .= "<b>Prove that you're a human:</b><br />";

        # Visual challenge
        unless ( $wants_audio || $POST{audio_chal} ) {
            $captcha_body .= "Type the letters and numbers you see below, to prove that you're not a spam robot. If you can't read the text, type 'AUDIO' and take a sound test instead.<br />";
            if ($capid && $anum) { # previously entered correctly
                $captcha_body .= "<img src='/captcha/image.bml?capid=$capid&amp;anum=$anum' width='175' height='35' />";
            } else {
                $captcha_body .= "<img src='/captcha/image.bml?chal=$captcha_chal&amp;try=$try' width='175' height='35' />";
            }
        }

        # Audio challenge
        else {
            $captcha_body .= "Type the numbers you hear to prove that you're not a spam robot: ";
            if ($capid && $anum) {
                $captcha_body .= "<a href='/captcha/audio.bml?capid=$capid&amp;anum=$anum'>Play the sound</a>";
            } else {
                $captcha_body .= "<a href='/captcha/audio.bml?chal=$captcha_chal&amp;try=$try'>Play the sound</a>";
            }
            $captcha_body .= LJ::html_hidden(audio_chal => 1);
        }

        $captcha_body .= "<br /><br />Answer: ";
        $captcha_body .= LJ::html_text({ name => 'answer', size => 15, value => $answer });
        $captcha_body .= LJ::html_hidden(captcha_chal => $captcha_chal);
        $captcha_body .= $inerr->('captcha', '<br />');
        $captcha_body .= "<br />";
    }

    unless (LJ::did_post()) {
        $body .= "<?p Want to invite someone to $LJ::SITENAMESHORT?<br /><br />";
        $body .= 'The link below will send your friend to the page where they can create a LiveJournal account.  Your journal will be automatically added as a friend and the page will list some of your active communities with the option to join. p?>';

        $body .= "<?p <div style='margin-left: 20px'><b>Link: <a href='$create_link'>$create_link</a></b></div> p?>";

        $body .= "<?p Enter your friend's name and email address in the form below and we'll send the invitation for you.  If you have multiple people to invite, you can use this form as often as you'd like, or just cut and paste the link and send it from your own email account. p?>";

        $body .= "<?p <b>Note:</b> $LJ::SITENAMESHORT doesn't store any of this after the invitation is sent. p?><br />";
    }

    $body .= "<?p <b>Fill out this form to invite someone to $LJ::SITENAMESHORT:</b> p?>";

    $body .= "<?p <form method='POST'>";
    $body .= "Your friend's name:";
    $body .= LJ::html_text({ name => "name", size => 40, value => $POST{name} }) . '<br />' . $inerr->("name", "<br />");

    $body .= "Your friend's email:";
    $body .= LJ::html_text({ name => "email", size => 40, value => $POST{email} }) . '<br />' . $inerr->("email", "<br />");

    $body .= 'Your message to them:<br />' . $inerr->("msg", "<br />");
    $body .= LJ::html_textarea({ rows => 20, cols => 80, name => "msg", value => $msg });
    $body .= '<br /><i>If you want to invite your friend to a specific community, you can mention the community name and URL here.</i><br /><br />';

    $body .= $captcha_body;

    $body .= '<br />' . LJ::html_submit("Invite 'em!") . '</form> p?>';

    return;
}
_code?><?page
title=><?_code return $title; _code?>
body=><?_code return $body; _code?>
page?>
