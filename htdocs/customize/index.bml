<?page
title=>Customize
body<=
<?_code
{
    use strict;
    use vars qw(%GET %POST %FORM);

    my $ret;
    my $remote = LJ::get_remote();

    unless ($remote) {
        return "<?h1 Login Required h1?><?p Before you customize your journal, you must first <a href='/login.bml?ret=1'>login</a>. p?>";
    }

    my @journals = ($remote->{'user'});
    push @journals, LJ::get_shared_journals($remote);

    my $journal = $GET{'journal'} || $remote->{'user'};
    unless (grep { $_ eq $journal } @journals) {  return BML::redirect("/customize/");  }


    my $u = $remote;
    $u = LJ::load_user($journal) unless $journal eq $remote->{'user'};
    my $userid = $u->{'userid'};
    
    return $LJ::MSG_READONLY_USER if LJ::get_cap($u, "readonly");
    
    if (@journals > 1) {
        $ret .= "<form method='get' action='./'>";
        $ret .= "<?h1 Customize Journal h1?><?p Work with journal: ";
        $ret .= LJ::html_select({
            'name' => 'journal',
            'selected' => $journal,
        }, map { $_, $_ } @journals);
        $ret .= " <input type='submit' value='Switch'> p?></form>";
    }
    
    LJ::load_user_props($u, "stylesys", "s2_style");
    $u->{'stylesys'} ||= 1;
    my $pub = LJ::S2::get_public_layers();
    my $userlay = LJ::S2::get_layers_of_user($u);
    my %style = LJ::S2::get_style($u, "verify");

    my $get_lang = sub {
        my $styleid = shift;
        foreach ($userlay, $pub) {
            return $_->{$styleid}->{'langcode'} if
                $_->{$styleid} && $_->{$styleid}->{'langcode'};
        }
        return undef;
    };
    my $langcode = $get_lang->($style{'i18n'}) || $get_lang->($style{'i18nc'});

    if ($POST{'save:stylesys'}) {
        my $num = $POST{'stylesys'} == 2 ? 2 : 1;
        LJ::set_userprop($u, "stylesys", $num);
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    my $implicit_style_create = sub {
        return if $u->{'s2_style'};
        unless ($u->{'s2_style'} = LJ::S2::create_style($u, "wizard")) {
            die "ERROR: Failed to create new style.";
        }
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, %style);
        LJ::set_userprop($u, "s2_style", $u->{'s2_style'});
    };

    if ($POST{'save:layout'}) {
        my $layid = $POST{'layoutid'};
        return BML::redirect("/customize/?journal=$u->{'user'}") if $layid == $style{'layout'};
        my $lay = $pub->{$layid} || $userlay->{$layid};
        return "Error: not your layout" unless $lay;
        my $coreid = $lay->{'b2lid'} or return "No core parent?";
        $implicit_style_create->();
        LJ::S2::delete_layer($style{'user'});
        $style{'user'} = $style{'theme'} = $style{'i18nc'} = $style{'i18n'} = 0;
        $style{'layout'} = $layid;
        $style{'core'} = $coreid;
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, %style);
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    if ($POST{'action:deluser'}) {
        LJ::S2::delete_layer($style{'user'});
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, "user", 0) if $style{'user'};
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    if ($FORM{'action:edituser'}) {
        $implicit_style_create->();
        my $id = $style{'user'};
        unless ($id) {
            $id = LJ::S2::create_layer($u, $style{'layout'}, "user");
            return "Error: couldn't generate user layer" unless $id;
            LJ::S2::set_style_layers($u, $u->{'s2_style'}, "user", $id);
        }
        return BML::redirect("/customize/layer.bml?w=user&journal=$journal");
    }

    if ($POST{'save:theme'}) {
        my $themeid = $POST{'themeid'}+0;
        my $lay = $pub->{$themeid} || $userlay->{$themeid};
        return "Error: not your theme" if $themeid && ! $lay;
        $implicit_style_create->();
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, "theme", $themeid);

        # TODO: conflict resolution.  check if there
        # exists a user layer, and ask user if they want to 
        # override it using the theme exclusively.
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    if ($POST{'save:langcode'}) {
        my $langcode = $POST{'langcode'};
        return BML::redirect("/customize/?journal=$u->{'user'}")
            if $langcode eq 'custom';

        my @langs = LJ::S2::get_layout_langs($pub, $style{'layout'});
        my ($i18n, $i18nc);

        # scan for an i18n user layer
        foreach (values %$userlay) {
            last if
                $_->{'b2lid'} == $style{'layout'} &&
                $_->{'type'} eq 'i18n' &&
                $_->{'langcode'} eq $langcode &&
                ($i18n = $_->{'s2lid'});
        }

        # scan for i18nc public layer and i18n layer if necessary
        foreach (values %$pub) {
            last if $i18nc && $i18n;

            next if
                ! $i18nc &&
                $_->{'type'} eq 'i18nc' &&
                $_->{'langcode'} eq $langcode &&
                ($i18nc = $_->{'s2lid'});

            next if
                ! $i18n && 
                $_->{'b2lid'} == $style{'layout'} &&
                $_->{'type'} eq 'i18n' &&
                $_->{'langcode'} eq $langcode &&
                ($i18n = $_->{'s2lid'});
        }

        $implicit_style_create->();
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, "i18nc", $i18nc);
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, "i18n", $i18n);

        # TODO: conflict resolution.  check if there
        # exists a user layer, and ask user if they want to 
        # override it using the theme exclusively.
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    # choose style system
    $ret .= "<form method='post' action='./?journal=$u->{'user'}'>";
    $ret .= "<?h1 Choose style system h1?><?p Do you want to use the old style system, or the new one? p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'stylesys', 'selected' => $u->{'stylesys'} },
                            1, "Old system (S1)",
                            2, "New system (S2)");
    $ret .= " <input type='submit' name='save:stylesys' value='Change'>";
    $ret .= "</blockquote>";

    # no more options if they're using S1.
    if ($u->{'stylesys'} == 1) {
        $ret .= "<?h1 Using S1 h1?><?p The old style system is configured through the <a href='/modify.bml'>Modify Journal</a> page. p?>";
        $ret .= "</form>";
        return $ret;
    }

    # choose layout
    my @layouts = map { $_, LJ::ehtml($pub->{$_}->{'name'}) }
                 sort { $pub->{$a}->{'name'} cmp $pub->{$b}->{'name'} }
                 grep { /^\d+$/ && $pub->{$_}->{'type'} eq "layout" &&
                        LJ::S2::can_use_layer($u, $pub->{$_}->{'uniq'})  } keys %$pub;
    # add user ones
    {
        my @user = map { $_, $userlay->{$_}->{'name'} ? $userlay->{$_}->{'name'} : "\#$_" } 
                   sort { $userlay->{$a}->{'name'} cmp $userlay->{$b}->{'name'} || 
                          $userlay->{$a}->{'s2lid'} <=> $userlay->{$b}->{'s2lid'} }
                   grep { /^\d+$/ && $userlay->{$_}->{'type'} eq "layout" }
                   keys %$userlay;
        if (@user) {
            push @layouts, "", "---", @user;
        }
    }
    $ret .= "<?h1 Step 1: Layout h1?><?p Pick your layout.  Because theme selection and custom modifications depend on your layout, changing this setting discards all other edits you may have made. p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'layoutid',
                              'selected' => $style{'layout'},  },
                            @layouts);
    $ret .= " <input type='submit' name='save:layout' value='Change'> (<a href=\"preview.bml?journal=$u->{'user'}\">previews</a>)";
    $ret .= "</blockquote>";

    # pick other stuff
    $ret .= "<?h1 Step 2: Customize Layout h1?>";
    $ret .= "<?p The following settings are layout-dependent. p?>";

    # langauge
    my @langs = LJ::S2::get_layout_langs($pub, $style{'layout'});

    # they have set a custom i18n layer
    if ($style{'i18n'} && 
        ($style{'i18nc'} != $style{'i18n'} || ! defined $pub->{$style{'i18n'}})) {
        push @langs, 'custom', '(Custom)';
        $langcode = 'custom';
    }
    
    $ret .= "<?h2 Language h2?><?p The following languages are fully or partially supported by this layout. p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'langcode',
                              'selected' => $langcode, },
                            @langs);
    $ret .= " <input type='submit' name='save:langcode' value='Change'>";
    $ret .= "</blockquote>";
    
    # theme
    my @themes = LJ::S2::get_layout_themes_select([$pub], $style{'layout'});
    # add user ones
    {
        my @user = map { $userlay->{$_}->{'s2lid'}, $userlay->{$_}->{'name'} ? 
                                                    $userlay->{$_}->{'name'} : 
                                                    "\#$userlay->{$_}->{'s2lid'}" }
                   sort { $userlay->{$a}->{'name'} cmp $userlay->{$b}->{'name'} ||
                          $userlay->{$a}->{'s2lid'} <=> $userlay->{$b}->{'s2lid'} }
                   grep { /^\d+$/ && $userlay->{$_}->{'b2lid'} == $style{'layout'} && $userlay->{$_}->{'type'} eq "theme" }
                   keys %$userlay;
        if (@user) {
            push @themes, "", "---", @user;
        }
    }

    $ret .= "<?h2 Theme h2?><?p Choose from the following optional themes for this layout. p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'themeid',
                              'selected' => $style{'theme'}, },
                            '0' => '(Layout Default)',
                            @themes);
    $ret .= " <input type='submit' name='save:theme' value='Change'> (<a href=\"themes.bml?journal=$u->{'user'}\">previews</a>)";
    $ret .= "</blockquote>";
    
    # customize
    $ret .= "<?h2 Change Individual Settings h2?><?p If you want to customize your journal further, here you can tweak individual settings to get it looking exactly how you want... p?>";
    $ret .= "<blockquote>";
    if ($style{'user'}) {
        $ret .= "<input type='submit' name='action:edituser' value='Edit Customizations...'>\n";
        $ret .= "<input type='submit' name='action:deluser' value='Remove Customizations'>\n";
    } else {
        $ret .= "<input type='submit' name='action:edituser' value='Customize...'>\n";
    }
    $ret .= "</blockquote>";

    $ret .= "</form>";

    return $ret;
}
_code?>
<=body
page?>
