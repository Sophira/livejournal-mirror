<?page
title=>Customize
body<=
<?_code
{
    use strict;
    use vars qw(%GET %POST %FORM);

    return LJ::server_down_html() if $LJ::SERVER_DOWN;

    my $dbs = LJ::get_dbs();
    my $dbh = $dbs->{'dbh'};
    my $dbr = $dbs->{'reader'};
    my $sth;

    return "Database temporarily unavailable." unless $dbh && $dbr;
    
    my $ret;
    my $remote = LJ::get_remote($dbs);

    unless ($remote) {
        return "<?h1 Login Required h1?><?p Before you customize your journal, you must first <a href='/login.bml?ret=1'>login</a>. p?>";
    }

    my @journals = ($remote->{'user'});
    push @journals, LJ::get_shared_journals($dbs, $remote);

    my $journal = $GET{'journal'} || $remote->{'user'};
    unless (grep { $_ eq $journal } @journals) {  return BML::redirect("/customize/");  }


    my $u = $remote;
    $u = LJ::load_user($dbs, $journal) unless $journal eq $remote->{'user'};
    my $userid = $u->{'userid'};
    
    my $dbcs = LJ::get_cluster_set($u);
    my $dbch = $dbcs->{'dbh'};
    my $dbcr = $dbcs->{'reader'};
    
    return $LJ::MSG_READONLY_USER if LJ::get_cap($u, "readonly");
    
    if (@journals > 1) {
        $ret .= "<form method='get'>";
        $ret .= "<?h1 Customize Journal h1?><?p Work with journal: ";
        $ret .= LJ::html_select({
            'name' => 'journal',
            'selected' => $journal,
        }, map { $_, $_ } @journals);
        $ret .= " <input type='submit' value='Switch'> p?></form>";
    }
    
    LJ::load_user_props($dbs, $u, "stylesys", "s2_style");
    $u->{'stylesys'} ||= 1;
    my $pub = LJ::S2::get_public_layers();
    my $userlay = {};   # TODO: add API call to fetch these
    my %style = LJ::S2::get_style($u->{'s2_style'});

    my $get_lang = sub {
        my $styleid = shift;
        foreach ($userlay, $pub) {
            return $_->{$styleid}->{'langcode'} if
                $_->{$styleid} && $_->{$styleid}->{'langcode'};
        }
        return undef;
    };
    my $langcode = $get_lang->($style{'i18n'}) || $get_lang->($style{'i18nc'});

    if ($POST{'save:stylesys'}) {
        my $num = $POST{'stylesys'} == 2 ? 2 : 1;
        LJ::set_userprop($dbh, $u, "stylesys", $num);
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    my $implicit_style_create = sub {
        unless ($u->{'s2_style'}) {
            $u->{'s2_style'} = LJ::S2::create_style($u, "wizard");
            LJ::set_userprop($dbh, $u, "s2_style", $u->{'s2_style'});
        }
    };

    if ($POST{'save:layout'}) {
        my $layid = $POST{'layoutid'};
        return BML::redirect("/customize/?journal=$u->{'user'}") if $layid == $style{'layout'};
        my $lay = $pub->{$layid} || $userlay->{$layid};
        return "Error: not your layout" unless $lay;
        my $coreid = $lay->{'b2lid'} or return "No core parent?";
        $implicit_style_create->();
        LJ::S2::delete_layer($style{'user'});
        $style{'user'} = $style{'theme'} = $style{'i18nc'} = $style{'i18n'} = 0;
        $style{'layout'} = $layid;
        $style{'core'} = $coreid;
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, %style);
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    if ($POST{'action:deluser'}) {
        LJ::S2::delete_layer($style{'user'});
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, "user", 0) if $style{'user'};
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    if ($FORM{'action:edituser'}) {
        $implicit_style_create->();
        my $id = $style{'user'};
        unless ($id) {
            $id = LJ::S2::create_layer($u, $style{'layout'}, "user");
            return "Error: couldn't generate user layer" unless $id;
            LJ::S2::set_style_layers($u, $u->{'s2_style'}, "user", $id);
        }
        return BML::redirect("/customize/layer.bml?w=user");
    }

    if ($POST{'save:theme'}) {
        my $themeid = $POST{'themeid'}+0;
        my $lay = $pub->{$themeid} || $userlay->{$themeid};
        return "Error: not your theme" if $themeid && ! $lay;
        $implicit_style_create->();
        LJ::S2::set_style_layers($u, $u->{'s2_style'}, "theme", $themeid);

        # TODO: conflict resolution.  check if there
        # exists a user layer, and ask user if they want to 
        # override it using the theme exclusively.
        return BML::redirect("/customize/?journal=$u->{'user'}");
    }

    # choose style system
    $ret .= "<form method='post'>";
    $ret .= "<?h1 Choose style system h1?><?p Do you want to use the old style system, or the new one? p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'stylesys', 'selected' => $u->{'stylesys'} },
                            1, "Old system (S1)",
                            2, "New system (S2)");
    $ret .= " <input type='submit' name='save:stylesys' value='Change'>";
    $ret .= "</blockquote>";

    # no more options if they're using S1.
    if ($u->{'stylesys'} == 1) {
        $ret .= "<?h1 Using S1 h1?><?p The old style system is configured through the <a href='/modify.bml'>Modify Journal</a> page. p?>";
        $ret .= "</form>";
        return $ret;
    }

    # choose layout
    my @layouts = map { $_, LJ::ehtml($pub->{$_}->{'name'}) }
                 grep { /^\d+$/ && $pub->{$_}->{'type'} eq "layout" &&
                        LJ::S2::can_use_layer($u, $pub->{$_}->{'uniq'})  } keys %$pub;
    $ret .= "<?h1 Step 1: Layout h1?><?p Pick your layout.  Because theme selection and custom modifications depend on your layout, changing this setting discards all other edits you may have made. p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'layoutid',
                              'selected' => $style{'layout'},  },
                            @layouts);
    $ret .= " <input type='submit' name='save:layout' value='Change'> (<a href=\"preview.bml?show=layouts&amp;journal=$u->{'user'}\">previews</a>)";
    $ret .= "</blockquote>";

    # pick other stuff
    $ret .= "<?h1 Step 2: Customize Layout h1?>";
    $ret .= "<?p The following settings are layout-dependent. p?>";

    # langauge
    my @langs = LJ::S2::get_layout_langs($pub, $style{'layout'});
    $ret .= "<?h2 Language h2?><?p The following languages are fully or partially supported by this layout. p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'langcode',
                              'selected' => $langcode, },
                            @langs);
    $ret .= " <input type='submit' name='save:langcode' value='Change'>";
    $ret .= "</blockquote>";
    
    # theme
    my @themes = LJ::S2::get_layout_themes([$pub, $userlay], $style{'layout'});
    $ret .= "<?h2 Theme h2?><?p Choose from the following optional themes for this layout. p?>";
    $ret .= "<blockquote>";
    $ret .= LJ::html_select({ 'name' => 'themeid',
                              'selected' => $style{'theme'}, },
                            '0' => '(Layout Default)',
                            @themes);
    $ret .= " <input type='submit' name='save:theme' value='Change'> (<a href=\"preview.bml?show=themes&amp;layid=$style{'layout'}\">previews</a>)";
    $ret .= "</blockquote>";
    
    # customize
    $ret .= "<?h2 Change Individual Settings h2?><?p If you want to customize your journal further, here you can tweak individual settings to get it looking exactly how you want... p?>";
    $ret .= "<blockquote>";
    if ($style{'user'}) {
        $ret .= "<input type='submit' name='action:deluser' value='Remove Customizations'>\n";
        $ret .= "<input type='submit' name='action:edituser' value='Edit Customizations...'>\n";
    } else {
        $ret .= "<input type='submit' name='action:edituser' value='Customize...'>\n";
    }
    $ret .= "</blockquote>";

    $ret .= "</form>";

#    use Data::Dumper;
#    $ret .= "<pre>" . Dumper($pub) . "</pre>";

    return $ret;
}
_code?>
<=body
page?>
